(LSMVersion 3.0.0)

(GlobalVars
	(nonoperative buyer_paid : ℕ := 0)
	(nonoperative seller_refunded : ℕ := 0)
	(writeonce delivery_date : Date)
)

(Claims
	($MAX_TIME = 44D)
	($MAX_EVENT_STATE_CHANGES := 6)
)

(Actors Buyer Seller)

(ContractParams
	(CONTRACT_START : Date := "2010-12-30")
	(DELIVERY_DEADlINE : Date := "2011-01-01")
)

(ProseContract
	(P1 'Seller agrees to transfer and deliver to Buyer, on or before 2011-01-01, the goods: 1 laser printer.')
	(P2 'Buyer agrees to accept the goods and to pay a total of €200 for them according to the terms further set out below.')
	(P3 'Buyer agrees to pay for the goods half upon receipt, with the remainder due within 30 days of delivery.')
	(P4 'If Buyer fails to pay the second half within 30 days, an additional fine of 10% has to be paid within 14 days.')
	(P5	'Upon receipt, Buyer has 14 days to return the goods to Seller in original, unopened packaging. Within 7 days thereafter, Seller has to repay the total amount to Buyer.')
)

(FormalContract
	"Two part sale Hvitved Laser Printer"
	(StartState Awaiting_Delivery)

	(NLGSection "Delivery and first instalment payment"
		(Event&State Awaiting_Delivery()
			(NLGSubsection "Deadline for delivery of the printer")
			[ProseRefs P1]
			(ActorEvents
				(Seller must
					(Deliver() by DELIVERY_DEADLINE)
				)
			)
		)

		(Action&State Deliver() [ProseRefs P1]
			(NLGSubsection "Delivery and payment of the first instalment")
			(NLGActionDescriptor "deliver the printer and pay the first instalment")
			(Entrance
				(delivery_date := (event_start_date))
				(conjecture (buyer_paid == 0))
				(buyer_paid := 100)
			)
			(ActorEvents
				(Buyer should
					(Pay_2nd_Installment() by (delivery_date + 30D))
				)
			)
			(Fallbacks
				(Late_2nd_Installment())
			)
		)
	)

	(NLGSection "Returns"
		(Action&State Return_On_Time() [ProseRefs P5]
			(NLGSubsection "Deadline for returning the printer")

			(Buyer may in states {Pay_1st_Installment Pay_2nd_Installment} by (delivery_date + 14D))

			(DependsOn Postmark_Refund)
		)

		(Action&State Postmark_Refund() [ProseRefs P5]
			(NLGSubsection "Deadline for refund")

			(Seller must in states {Return_On_Time} by (today() + 7D))

			(Entrance
				(conjecture (seller_refunded == 0))
				(conjecture ((buyer_paid == 100) or (buyer_paid == 200)))
				(seller_refunded := buyer_paid)
			)
			(Fallbacks
				(Fulfilled())
			)
		)
	)

	(NLGSection "Payment"

		(Event&State Late_2nd_Installment() [ProseRefs P4]
			(NLGSubsection "Late payment of second instalment")
			(ActorEvents
				(Buyer must
					(Pay_2nd_Installment_With_Late_Fee() by (delivery_date + 44D))
				)
			)
		)
	)

	(NLGSection "Completion of sale"
		(Action&State Pay_2nd_Installment() [ProseRefs P5]
			(NLGSubsection "Completion of contract following on-time payment of the second instalment")
			(NLGActionDescriptor "pay the second instalment")
			(Entrance
				(conjecture (buyer_paid == 100))
				(buyer_paid := 200)
			)

			(Fallbacks
				(Fulfilled())
			)
		)

		(Action&State Pay_2nd_Installment_With_Late_Fee() [ProseRefs P4]
			(NLGSubsection "Completion of contract following late payment of the second instalment")
			(Entrance
				(conjecture (buyer_paid == 100))
				(buyer_paid := 220)
			)
			(Fallbacks
				(Fulfilled())
			)
		)
	)
)

(DotFileName 'hvitved_printer.dot')
(ImgFileName 'hvitved_printer.png')