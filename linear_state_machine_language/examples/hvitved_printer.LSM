(GlobalVars 
	(writeonly buyer_paid : ℕ ≔ 0)
	(writeonly seller_refunded : ℕ ≔ 0)
)

(Claims 
	($MAX_TIME = 44D)
	($MAX_EVENT_STATE_CHANGES = 6)
)

(Actors Buyer Seller)

(ProseContract 
	(P1 'Seller agrees to transfer and deliver to Buyer, on or before 2011-01-01, the goods: 1 laser printer.')
	(P2 'Buyer agrees to accept the goods and to pay a total of €200 for them according to the terms further set out below.')
	(P3 'Paragraph 3. Buyer agrees to pay for the goods half upon receipt, with the remainder due within 30 days of delivery.')
	(P4 'If Buyer fails to pay the second half within 30 days, an additional fine of 10% has to be paid within 14 days.')
	(P5	'Upon receipt, Buyer has 14 days to return the goods to Seller in original, unopened packaging. Within 7 days thereafter, Seller has to repay the total amount to Buyer.')
)

(FormalContract	
	"Two part sale Hvitved Laser Printer"
	(Parameters 
		(DELIVERY_DEADlINE : Date)
	)
	{EventStates
		(Start() [P1] (
			(Seller 
				(Delivered() by DELIVERY_DEADLINE)
			)
		))
	
		(Delivered() [P1] (
			(Code
				(writeonce delivery_date ≔ today)
			)
			(Buyer 
				(Paid_1st_Installment(delivery_date) immediately)
			)
		))

		(Paid_1st_Installment(delivery_date:Date, othervar:OtherSort) [P1 P2 P3] (
			(Code
				(conjecture buyer_paid = 0)
				(buyer_paid ≔ 100)
			)
			(Buyer 
				(Paid_2nd_Installment(delivery_date) by (delivery_date + 30D))
				(Returned_On_Time(delivery_date) by (delivery_date + 14D))
			)
			(AfterDeadlines 
				(Late_2nd_Installment(delivery_date))
			)
		))

		(Late_2nd_Installment(delivery_date:Date) [P4] (
			(Buyer 
				(Late_Fee_Paid_And_Sale_Complete() by (delivery_date + 44D))
			)
		))
		
		(Paid_2nd_Installment(delivery_date:Date) [P5] (
			(Code
				(conjecture buyer_paid = 100)
				(buyer_paid ≔ 200)
			)
			(Buyer 
				(Returned_On_Time() by (delivery_date + 14D))
			)
			(AfterDeadlines 
				(Normal_Sale_Ended())
			)
		))

		(Returned_On_Time() [P5] (
			(Seller 
				(Refund_Received() by (today() + 7D))
			)
		))

		(Late_Fee_Paid_And_Sale_Complete() [P4] (
			(Code 
				(conjecture buyer_paid = 100)
				(buyer_paid ≔ 220)
			)
			(AfterDeadlines 
				(Fulfilled())
			)
		))

		(Normal_Sale_Ended() [P2] (
			(AfterDeadlines 
				(Fulfilled())
			)
		))

		(Refund_Received() [P5] (
			(Code
				(conjecture seller_refunded = 0)
				(conjecture (buyer_paid = 100) or (buyer_paid = 200))
				(seller_refunded ≔ buyer_paid)
			)
			(AfterDeadlines 
				(Fulfilled())
			)
		))
	}
)

(DotFileName 'printer_state_diagram.dot')