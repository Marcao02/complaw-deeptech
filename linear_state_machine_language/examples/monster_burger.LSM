(GlobalVars 	
	(writeonce challenge_start : Time )
	(writeonce challenge_time_exhausted : Time)
	(amount_owing : Price ≔ 0)
)

(Actors Challenger Restaurant)

(Parameters 
	(MB_PRICE : Price)
	(TIME_TO_EAT_BURGER : Duration)
	(MAX_SERVE_DELAY : Duration)
	(MAX_CLAIM_VERIFICATION_DELAY : Duration = 30M)
	(MAX_REFUND_DELAY : Duration)
)

(ProseContract 
	(P1 'A monster burger (MB) challenge is between a customer and the Restaurant.')
	(P2 'A customer can only enter one MB challenge per visit to the Restaurant.')
	(P3 'If the customer finishes the MB within {TIME_TO_EAT_BURGER} of being served, the burger is free. Otherwise, they owe $50 for the burger.')
	(P4 'When a customer orders a MB, the restaurant guarantees they will serve the burger within {MAX_SERVE_DELAY}. Otherwise, the customer is not obligated to pay for the burger (but the restaurant may still serve the burger late).')
	(P5 'The customer can claim to be finished early, after which the restaurant must within ({MAX_CLAIM_VERIFICATION_DELAY}, or by the end of the {TIME_TO_EAT_BURGER} challenge period -- whichever comes first) confirm or reject the claim. If no such "early finish" claim is made, the restaurant checks after {TIME_TO_EAT_BURGER} from serving time')
	(P6 'Any other food or drink that the customer wishes to consume while doing the MB challenge must be ordered on a separate bill.')
)

(FormalContract	
	"Monster Burger"		
	(StartState MonsterBurgerOrdered)
	(EventStates
		(MonsterBurgerOrdered() [P4] (
			(Code
				(amount_owing := MB_PRICE)
			)
			(Restaurant 
				(ServeMB() within MAX_SERVE_DELAY)
			)
			(AfterDeadlines
				(MissedServeDeadline())
			)
		))		
	
		(ServeMB() [] (
			(Code				
				(challenge_start ≔ now())
				(challenge_time_exhausted ≔ (challenge_start + {TIME_TO_EAT_BURGER}))
			)			
			(AfterDeadlines
				(EatingMB())
			)
		))


		(MissedServeDeadline() [] (
			(Code
				(amount_owing := 0)
			)			
			(AfterDeadlines
				(Fulfilled())
			)

		))

		(EatingMB() [] (
			(Challenger
				(ChallengerAnnouncesFinished() before challenge_time_exhausted)
			)
			(Restaurant
				(CheckFinishedClaim() at challenge_time_exhausted)
			)
		))		

		(ChallengerAnnouncesFinished() [] (
			(Restaurant
				(CheckFinishedClaim() by (earliest (now() + MAX_CLAIM_VERIFICATION_DELAY) challenge_time_exhausted))
			)
			(AfterDeadlines
				(LateCheck())
			)
		))

		(LateCheck() [] (
			(Code
				(amount_owing := 0)
			)
			(AfterDeadlines
				(Fulfilled())
			)

		))

		(CheckFinishedClaim() [] (
			(Restaurant
				(VerifyFinishedClaim() immediately)
				(RejectFinishedClaim() immediately)
			)
		))

		(RejectFinishedClaim() [] (
			(AfterDeadlines
				(EatingMB())
			)
		))

		(VerifyFinishedClaim() [] (
			(Code
				(amount_owing := 0)
			)
			(AfterDeadlines
				(Fulfilled())
			)			
		))
	)
)

(DotFileName 'monster_burger.dot')
(ImgFileName 'monster_burger.png')