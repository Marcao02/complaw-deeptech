(GlobalVars 
	( nonoperative units_ordered : ℕ := 0 )	
	( nonoperative inc units_delivered : ℕ := 0 )
	( writeonce contract_ends : Date )	
	( orderQ : Queue[Pair[ℕ,Deadline]] := emptyQueue() ) 
)

(Actors Buyer Seller)

(ContractParams 
	(MAX_UNITS : ℕ := 1000)
	(CONTRACT_LIFE : Date := 1Y)
)

(ReactiveVars	
	(orderable_units : ℕ := (MAX_UNITS - units_ordered))
)

(ProseContract 
	(P1 'The master agreement between Vendor and Customer is for 1000 printers, with a unit price of e100. The agreement is valid for one year, starting 2011-01-01.')
	(P2 'The customer may at any time order an amount of printers (with the total not exceeding the threshold of 1000), after which the Vendor must deliver the goods before the maximum of (i) 14 days, or (ii) the number of ordered goods divided by ten days.')
	(P3 'After delivering the goods, Vendor may invoice the Customer within 1 month, after which the goods must be paid for by Customer within 14 days.')	
; )

(FormalContract	
	"Master Sales Agreement"	
	(StartState Start)
	(EventStates
		(Start()
			(Entrance
				(contract_ends := (contractStartDay + 1Y))
			)
			(Fallbacks
				(ContractLive())
			)			
		)

		(ContractLive()
			(ActorEvents
				(Buyer mayif (orderable_units > 0)
					(Order(_) by contract_ends)
				)
				(Seller mustif (nonempty orderQ)
					; fst (top orderQ) is the quantity, snd (top orderQ) is the deadline
					(Deliver((fst (top orderQ))) by (snd (top orderQ)))
				)
			)
			(Fallbacks
				; this will only be executed when both of actions in ActorEvents are disabled							
				(Fulfilled())
			)
		)

		(Order(quantity : ℕ)			
			(Entrance
				(delivery_deadline := (max 14D ((ceil (quantity/10))D)) )				
				(units_ordered += quantity )
				(orderQ := (enqueue orderQ (quantity delivery_deadline)) )
			)						
			(Fallbacks
				(ContractLive())
			)
		)

		(Deliver(quantity : ℕ)
			(Entrance
				(units_delivered += quantity)
			)
			(Fallbacks
				(ContractLive())
			)
		)
	)
)

(DotFileName 'hvitved_master_sales_agreement.dot')
(ImgFileName 'hvitved_master_sales_agreement.png')