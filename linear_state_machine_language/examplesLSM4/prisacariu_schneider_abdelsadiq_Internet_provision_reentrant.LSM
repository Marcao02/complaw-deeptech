(LSMVersion 4.0.0)

; These are some properties we would want to verify are true:
; Client, when their usage is high, should always have the permission to lower it. This one led me to catch a bug in an earlier version of the contract.
; Client, when they owe money, should always have the permission to pay.
; Client, if they lower within 24H of infraction AND pay 1 fee unit within 24H of infraction, owes nothing more. This is regardless of whether they send a delay-payment email.
; Client, if they neither pay within 24H nor send a delay-payment email within 24H, breaches the contract.
; Client, if they send delay email and lower traffic within 24H of infraction, and don't pay within 24 hours, then they owe and must eventually (say, within 48H of infraction) pay 2 fee units for this infraction.
; Client, if they send delay email and lower traffic between 24H and 48H after infraction, they owe and must eventually (say, within 48H of infraction) pay 3 fee units for this infraction.
; Client may pay in up to 2 parts, one before 24H and the other between 24H and 48H.
; Client, if they don't lower traffic within 48 hours of infraction, breaches the contract.

; NOTE 1: If Client overpays, this contract does not have any facility for refunding them (but it could easily be added).
; NOTE 2: This contract ignores the possibility of a message being sent but not received.
; NOTE 3: It’s not quite ideal between instances of the client fulfilling their obligations and the next time when they can next raise their internet traffic. Specifically, they can only have high traffic in one interval within each 48 hour interval. Also, the client must finish paying for their last high-usage infraction before raising the usage again.

(FormalContract
	"PS Internet Provision"

	(Actors ISP Client)

	(ContractParams
		(HIGH_USAGE_FEE : ℕ := 10)
		(HOURS_PARAM : ℕ := 24)
	)

	; The original (ambiguous) agreement.
	(ProseContract
		(P1 'Whenever Internet traffic is high then the client must pay {HIGH_USAGE_FEE} or the client must notify the service provider by sending an e-mail specifying that he will pay later within {HOURS_PARAM} hours.')
		(P2 'In case the client delays the payment, after notification he must lower Internet traffic within {HOURS_PARAM} hours to the low level, and pay twice the amount {HIGH_USAGE_FEE}.')
		(P3 'If the client does not lower the Internet traffic within {HOURS_PARAM} hours, then the client will have to pay thrice the amount {HIGH_USAGE_FEE}.')
		(P4 'The provider does not have permission to cancel the contract without previous written notification by normal post and by e-mail.')
	)

	(GlobalVars
		(contract_canceled : Bool = false)
		(cancel_notice_sent_by_post : Bool = false)
		(cancel_notice_sent_by_email : Bool = false)
		(L24 : Bool = false) ; lowered traffic within 24H of infraction
		(L48 : Bool = false) ; lowered traffic between 24 and 48 hours of infraction
		(sent_delay_email : Bool = false) ; sent delay-payment-notification email within 24H of infraction.
		(P1 : Bool = false) ; paid 1X high usage fee within 24H of infraction
		(P2 : Bool = false) ; paid at least 2X high usage fee between 24 and 48 hours of infraction
		(P3 : Bool = false) ; paid 3X high usage fee between 24 and 48 hours of infraction
	)

	(StartState TrafficNormal)

	(Action CancelContract
		(AllowedSubjects ISP)
		; ISP can do this action in any contract state provided both of the
		; cancel_* boolean variables are true
		(ISP may in any state if (and cancel_notice_sent_by_email cancel_notice_sent_by_post))
		(Entrance
			(contract_canceled = true)
		)
	)

	(Event&State TrafficNormal
		(Entrance
			(L24 = false)
			(sent_delay_email = false)
			(P1 = false)
			(P2 = false)
			(P3 = false)
		)
		(Client may next (TrafficRaised))
	)

	(Action LowerTrafficWithin24HofInfraction
		(AllowedSubjects Client)
		(Entrance
			(L24 := true)
		)
		(startContract "PS Internet Provision" (Actors ISP Client))
	)

	(Action SendDelayEmail
		(AllowedSubjects Client)
		(Entrance
			(sent_delay_email := true)
		)
	)

	(Action Pay1XHighUsageFeeWithin24HofInfraction
		(AllowedSubjects Client)
		(Entrance
			(P1 := true)
		)
	)

	(Action&State TrafficRaised
		(verifiedDisjointExhaustive
			(if (within? 24H)
				(Client may next (LowerTrafficWithin24HofInfraction))
				(Client may next (SendDelayEmail))
				(Client may next (Pay1XHighUsageFeeWithin24HofInfraction))
			)
			(if (not (within? 24H))
				(After24H)
			)
		)
	)

	(Event&State After24H
		(verifiedDisjoint
			(if (and P1 L24)
				; Both paid 1X high usage fee and lowered traffic within 24H.
				; They owe nothing more.
				(ContingentFulfilled)
			)
			(if (and (not P1) sent_delay_email L24)
				; Sent the delay payment email, and lowered traffic within 24H.
				; They owe 2X high usage fee.
				(if (and (not P2) (not P3))
					(Client must later (within 24H) (Pay2XHighUsageFeeBetween24And48HofInfraction))
				)
				(ContingentFulfilled)
			)
			(if (and P1 (not L24))
				; Paid 1X high usage fee within 24H and doesn't lower traffic within 24H.
				; Regardless of whether they sent the delay payment email,
				; they owe 3X high usage fee total, so must pay an additional 2X.
				(if (and (not P2) (not P3))
					(Client must later (within 24H) (Pay2XHighUsageFeeBetween24And48HofInfraction))
				)
				(if (not L48)
					(Client must later (within 24H) (LowerTrafficBetween24And48HofInfraction))
				)
				(ContingentFulfilled)
			)
			(if (and (not P1) sent_delay_email (not L24))
				; Sent the delay payment email and doesn't lower traffic within 24H.
				; They owe 3X high usage fee.
				(if (not P3)
					(Client must later (within 24H) (Pay3XHighUsageFeeBetween24And48HofInfraction))
				)
				(if (not L48)
					(Client must later (within 24H) (LowerTrafficBetween24And48HofInfraction))
				)
				(ContingentFulfilled)
			)
		)

		(if (and (not L48) (not L24))
			(Client may next (within 24H) LowerTrafficBetween24And48HofInfraction)
		)
		(if (and (not P2) (not P3))
			(Client may next (within 24H) Pay2XHighUsageFeeBetween24And48HofInfraction)
			(Client may next (within 24H) Pay3XHighUsageFeeBetween24And48HofInfraction)
		)

		(if contract_canceled
			(ContingentFulfilled)
		)
	)

	(Action Pay2XHighUsageFeeBetween24And48HofInfraction
		(AllowedSubjects Client)
		(Entrance
			(P2 := true)
		)
	)

	(Action Pay3XHighUsageFeeBetween24And48HofInfraction
		(AllowedSubjects Client)
		(Entrance
			(P3 := true)
		)
	)

	(Action LowerTrafficBetween24And48HofInfraction
		(AllowedSubjects Client)
		(Entrance
			(L48 := true)
		)
		(startContract "PS Internet Provision" (Actors ISP Client))
	)


)
