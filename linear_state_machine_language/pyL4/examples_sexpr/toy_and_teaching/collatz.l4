; This is a silly degenerate "contract" with only the environment role Env.
; It hints at how one would prove that the (traversals == N), (x ≤ traversals ≤ y)
; etc proof obligations are undecidable. For this one problem, a decision procedure
; would resolve the famous Collatz Conjecture.

(TimeUnit d)

(ContractParams
	(START : ℕ = 99)
)

(GlobalVars
	(x : ℕ = START)
)

(Roles) ; only the environment Env

(FormalContract "Collatz"
	(StartSection Ongoing)

	(Section Ongoing
		(Next
			(if ((x == 0) or (x == 1))
				(EnterFulfilled immediately)
			)
			(if ((x > 1) and (even x))
				(DivideBy2 immediately)
			)
			(if ((x > 1) and (odd x))
				(TripplePlus1 immediately)
			)
		)
	)

	(Action DivideBy2
		(TransitionsTo Ongoing)
		(StateTransform
			(x = (round (x / 2)))
		)
	)

	(Action TripplePlus1
		(TransitionsTo Ongoing)
		(StateTransform
			(x = ((3 * x) + 1))
		)
	)

	(Action EnterFulfilled
		(traversals == 1)
		(TransitionsTo Fulfilled)
	)

)
