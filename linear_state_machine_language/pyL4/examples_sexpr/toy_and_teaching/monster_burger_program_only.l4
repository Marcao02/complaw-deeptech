; See monster_burger.l4 for version of this with more comments and an NLG mockup.

(GlobalVars
	(writeonce challenge_endlimit_timestamp : Timestamp )
	(amount_owing                      		: Price = 0)
	(amount_paid                       		: Price = 0)
)

(Roles Challenger Restaurant)

(TimeUnit m)

(ContractParams
	(MB_PRICE                     : Price    = 50)
	(TIME_TO_EAT_BURGER           : Duration = 1h)
	(MAX_SERVE_DELAY              : Duration = 20m)
	(MAX_CLAIM_VERIFICATION_DELAY : Duration = 10m)
	(MAX_REFUND_DELAY             : Duration = 10m)
)

(ProseContract
	(P1 'A monster burger (MB) challenge is between a customer and the Restaurant.')
	(P2 'A customer can only enter one MB challenge per visit to the Restaurant.')
	(P3 'If the customer finishes the MB within {TIME_TO_EAT_BURGER} of being served, the burger is free. Otherwise, they owe {MB_PRICE} for the burger.')
	(P4 'When a customer orders a MB, the restaurant guarantees they will serve the burger within {MAX_SERVE_DELAY}. Otherwise, the customer is not obligated to pay for the burger (but the restaurant may still serve the burger late).')
	(P5 'The customer can claim to be finished early, upon which the restaurant must within ({MAX_CLAIM_VERIFICATION_DELAY}, or by the end of the {TIME_TO_EAT_BURGER} challenge period -- whichever comes first) confirm or reject the claim. If no such "early finish" claim is made, the restaurant checks after {TIME_TO_EAT_BURGER} from serving time')
	(P6 'Any other food or drink that the customer wishes to consume while doing the MB challenge must be ordered on a separate bill.')
)

(FormalContract
	"Monster Burger"
	(StartSection MonsterBurgerUncooked)

	(Section MonsterBurgerUncooked
		(Next
			(Challenger must (RequestCookMB) (event_td ≤ (sectionEntrance_td + 30m)))
		)
	)

	(Action RequestCookMB
	    ;  LANG note: static checker directive, which tells the static checker to prove that this action occurs exactly once in any execution that ends in Fulfilled
		(traversals == 1)
		(AllowedSubjects Challenger)

		(StateTransform
			(amount_owing = MB_PRICE)
		)

		(FollowingSection
			(visits == 1) ; LANG note: Similar to (traversal == 1) but for Sections.
			(Next
	            (timeConstraintsPartitionFuture
		            (Restaurant should (ServeMB) (event_td < (MAX_SERVE_DELAY + sectionEntrance_td)))
		            (EnterPromptServeGuaranteeViolated (event_td ≥ (MAX_SERVE_DELAY + sectionEntrance_td)))
	            )
	        )
		)
	)


	(Action ServeMB
		(TransitionsTo EatingMB)
		(0 ≤ traversals ≤ 1)
		(AllowedSubjects Restaurant)

		(StateTransform
			(challenge_endlimit_timestamp = (TIME_TO_EAT_BURGER unitsAfter sectionEntrance_td))
		)
	)


	(Action EnterPromptServeGuaranteeViolated
		(TransitionsTo Fulfilled)
		(0 ≤ traversals ≤ 1)
		(AllowedSubjects Env)
		(StateTransform
			(amount_owing = 0)
		)
	)

	(Action EnterEatingMB
		(0 ≤ traversals ≤ nonconstant) ; LANG note: nonconstant means can't be bounded by any numeral
		(AllowedSubjects Env)
	)
	(Section EatingMB
		(0 ≤ visits ≤ nonconstant)

        (Next
            (timeConstraintsPartitionFuture ; LANG note: `timeConstraintsPartitionFuture` will tell the static checker to verify that the deadline expressions partition the future.
	            (Challenger may (AnnounceMBFinished) (event_td < challenge_endlimit_timestamp))
	            ;(Restaurant should (CheckCompletionClaim) (nonstrictly-after-td-and-within challenge_endlimit_timestamp MAX_CLAIM_VERIFICATION_DELAY))
	            (Restaurant should (CheckCompletionClaim) ((challenge_endlimit_timestamp ≤ event_td) and (event_td ≤ (MAX_CLAIM_VERIFICATION_DELAY + sectionEntrance_td))))
	            (TimeToCheckCompletionExpires (event_td == (MAX_CLAIM_VERIFICATION_DELAY unitsAfter challenge_endlimit_timestamp)))
            )
        )
	)

	(Action AnnounceMBFinished
		(0 ≤ traversals ≤ nonconstant)
		(AllowedSubjects Challenger)

		(FollowingSection
			(0 ≤ visits ≤ nonconstant)
			(Next
	            (timeConstraintsPartitionFuture
		            (Restaurant should (CheckCompletionClaim) (event_td ≤ (sectionEntrance_td + MAX_CLAIM_VERIFICATION_DELAY)))
		            (TimeToCheckCompletionExpires (event_td == (MAX_CLAIM_VERIFICATION_DELAY unitsAfter sectionEntrance_td)))
	            )
	        )
		)
	)

	(Action TimeToCheckCompletionExpires
		(TransitionsTo Fulfilled)
		(0 ≤ traversals ≤ 1)
		(AllowedSubjects Env)
		(StateTransform
			(amount_owing = 0)
		)
	)

	(Action CheckCompletionClaim
		(0 ≤ traversals ≤ nonconstant)
		(AllowedSubjects Restaurant)

		(FollowingSection
			(0 ≤ visits ≤ nonconstant)
			(Next
			    (Restaurant weakly-must (VerifyCompletionClaim) immediately)
			    (Restaurant weakly-must (RejectCompletionClaim) immediately)
			)
		)
	)


	(Action RejectCompletionClaim
		(0 ≤ traversals ≤ nonconstant)
		(AllowedSubjects Restaurant)

		(FollowingSection
	        (0 ≤ visits ≤ nonconstant)
	        (Next
	        	; LANG note: `guardsDisjointExhaustive` will tell the static checker to verify that it is
	            ; always the case that exactly one of the tests of the children conditionals is true.
	            (guardsDisjointExhaustive
	            	; I think we should encourage this style of order-insensitive conditionals.
		            (if (sectionEntrance_td ≤ challenge_endlimit_timestamp)
		                (EnterEatingMB immediately)
		            )
		            (if (sectionEntrance_td > challenge_endlimit_timestamp)
		                (Challenger must (PayForMB) immediately)
		            )
	            )
	        )
		)
	)

	(Action PayForMB
		(TransitionsTo Fulfilled)
		(0 ≤ traversals ≤ 1)
		(AllowedSubjects Challenger)

		(StateTransform
			(amount_paid = amount_owing)
			(amount_owing = 0)
		)
	)


	(Action VerifyCompletionClaim
		(TransitionsTo Fulfilled)
		(0 ≤ traversals ≤ 1)
		(AllowedSubjects Restaurant)

		(StateTransform
			(amount_owing = 0)
		)
	)



)

; don't use these options for examples in the repo. a canonical name will be chosen.
; (DotFileName 'monster_burger.dot')
; (ImgFileName 'monster_burger.png')