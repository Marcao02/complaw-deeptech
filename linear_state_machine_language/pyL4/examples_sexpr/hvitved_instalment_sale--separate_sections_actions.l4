
(Roles Buyer Seller)

(ContractParams
	(INTEREST_RATE : ℚ = .1)
	(TOTAL_DUE_BEFORE_CLOSING : ℕ = 9500)
	(MIN_PAYMENT : ℕ = 500)
	(DUE_AT_CLOSING : ℕ = 500)
)

(GlobalVars
	(balance_remaining = TOTAL_DUE_BEFORE_CLOSING)
	(contract_month = 0)
)

(ProseContract
	(P1 'Buyer agrees to pay to Seller the total sum e10000, in the manner following:')
	(P2 'e500 is to be paid at closing, and the remaining balance of e9500 shall be paid as follows:')
	(P3 'e500 or more per month on the first day of each and every month, and continuing until the entire balance, including both principal and interest, shall be paid in full -- provided, however, that the entire balance due plus accrued interest and any other amounts due here-under shall be paid in full on or before 24 months.')
	(P4 'Monthly payments shall include both principal and interest with interest at the rate of 10%, computed monthly on the remaining balance from time to time unpaid.')
)

(FormalContract
	"Instalment Sale Hvitved"
	(StartSection WaitingForFirstDayOfNextMonth)

	(Action EnterWaitingForFirstDayOfNextMonth
		(TransitionsTo WaitingForFirstDayOfNextMonth)
	)

	(Section WaitingForFirstDayOfNextMonth
		(Next
			; note contract says payments happen on the first day of the month, not on-or-before.
			; so I am being lazy and interpreting it literally.
			(if (contract_month < 23)
				(Buyer must (PayInstallment a)
					(on (dateFromDayInMonthIndexAndMonthIndex 1 (nextMonthIndex)))
					(where ((MIN_PAYMENT ≤ a ) and (a ≤ balance_remaining))))
			)

			(if (contract_month == 23)
				(Buyer must (PayLastInstallment a)
					(on (dateFromDayInMonthIndexAndMonthIndex 1 (nextMonthIndex)))
					(where (a == (balance + DUE_AT_CLOSING))))
			)
		)
	)

	(Action (PayInstallment(amount:ℕ))
		(TransitionsTo AfterPayInstallment)
		(Entrance ; neither of the conjecture statements is necessary
			(conjecture (event_start_date == (monthStartDay)))
			(conjecture (amount ≤ balance)) ; overpaying is forbidden
			(balance_remaining -= amount)
			(balance_remaining *= (1 + INTEREST_RATE))
		)
	)

	(Section AfterPayInstallment
		(Next
			(WaitingForFirstDayOfNextMonth immediately)
		)
	)

	(Action (PayLastInstallment(amount:ℕ))
		(TransitionsTo AfterPayLastInstallment)
		(Entrance ; neither of these statements is necessary
			(conjecture (amount == (balance + DUE_AT_CLOSING)))
			(balance = 0)
		)
	)

	(Section AfterPayLastInstallment
		(Next
			(Fulfilled immediately)
		)
	)
)