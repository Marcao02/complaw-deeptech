
; QUESTION: Can the debt be repaid before maturity? If not, then the interest rate + maturity length is just an obtuse way of saying what will be owed if investor chooses payout.

; QUESTION: In Acquisition event if investor chooses 2x payment ("(b) get paid a multiple on the original investment."), does that mean the interest rate has no effect? NO -->

; TODO: "No Fractional Shares"

(Roles Investor Company)
(TimeUnit d)

(ContractParams
	(PURCHASE_AMOUNT : Pos$ = 100000)

	(TIME_TILL_MATURITY : TimeDelta = (days (365 + (364 / 2))))
	(VALUATION_CAP : Pos$ = inf)
	(DISCOUNT_RATE : "(0,1]" = 1)
	(INTEREST_RATE : "[0,1]" = .04)
	(DEBT_VERSION : Bool = true) ; bug "SyntaxError: Don't recognize name INTEREST_RATE"

	(START_INVESTOR_COMMON_STOCK : Shares = 0)
	(START_INVESTOR_PREFERRED_STOCK : Shares = 0)
	(START_INVESTOR_SERIES_SEED_PREFERRED_STOCK : Shares = 0)
	(START_INVESTOR_CASH : $ = 0)

	(START_COMPANY_CASH : $ = 0)
)

(GlobalVars
	(investor_cash : $ = START_INVESTOR_CASH)
	(investor_Common_Stock : Shares = START_INVESTOR_COMMON_STOCK)
	(investor_PreferredStock : Shares = START_INVESTOR_PREFERRED_STOCK)
	(investor_SeriesSeedPreferredStock : Shares = START_INVESTOR_SERIES_SEED_PREFERRED_STOCK)

	(company_cash : $ = START_COMPANY_CASH)

	(writeAtMostOnce pay_interest_in_cash : Bool = false)
)


(Macro principle_and_compound_interest (#principle #ir #full_periods #final_period_fraction)
	((#principle * ((1 + #ir) ^ #full_periods)) + (((#principle * ((1 + #ir) ^ #full_periods)) * #ir) * #final_period_fraction))
)

(Macro compound_interest (#principle #ir #full_periods #final_period_fraction)
	(cast Pos$ ((apply-macro principle_and_compound_interest (#principle #ir #full_periods #final_period_fraction)) - #principle))
)

(Macro interest_from_td #td
	(ifthenelse (#td ≤ 365d)
		(apply-macro compound_interest (PURCHASE_AMOUNT INTEREST_RATE 0 (#td / 365d)))
		(apply-macro compound_interest (PURCHASE_AMOUNT INTEREST_RATE 1 ((cast PosTimeDelta (#td - 365d)) / 365d)))
	)
)

(FormalContract "KISS"
	(StartSection InvestorInvests)

	(Section InvestorInvests
		(Next
			(Company obligation-options-include (CommitToAcquisition ?1) (next_event_td < (sectionEntrance_td + TIME_TILL_MATURITY)))
			(Company obligation-options-include (CommitToFinancing ?1) (next_event_td < (sectionEntrance_td + TIME_TILL_MATURITY))) ; IPO
			(ReachMaturity (next_event_td ≥ (sectionEntrance_td + TIME_TILL_MATURITY)))
		)
	)

	; ((ifthenelse (event_td ≤ 365d) (((PURCHASE_AMOUNT * ((1 + INTEREST_RATE) ^ 0)) + (((PURCHASE_AMOUNT * ((1 + INTEREST_RATE) ^ 0)) * INTEREST_RATE) * (event_td / 365d))) - PURCHASE_AMOUNT) (((PURCHASE_AMOUNT * ((1 + INTEREST_RATE) ^ 1)) + (((PURCHASE_AMOUNT * ((1 + INTEREST_RATE) ^ 1)) * INTEREST_RATE) * ((cast PosTimeDelta (event_td - 365d)) / 365d))) - PURCHASE_AMOUNT)) + (2 * PURCHASE_AMOUNT))

	(Action (CommitToFinancing
				(qualifying_round_new_money : $)
				)
		; Automatic conversion to preferred stock when you raise a qualifying priced round.
		; Conversion price = lesser of cap or discount.
		(AllowedSubjects Company)
		(Pre (qualifying_round_new_money ≥ 1000000))

		(FollowingSection
			(Next
				(Company must NotifyInvestorOfNextEquityFinancingTerms no_time_constraint)
			)
		)
	)

	(Action NotifyInvestorOfNextEquityFinancingTerms
		(AllowedSubjects Company)
		(TransitionsTo After_NotifyInvestorOfNextEquityFinancingTerms)
	)

	(Section After_NotifyInvestorOfNextEquityFinancingTerms
		(Next
			(Investor may choose_cash_interest_repayment no_time_constraint)
			(Company obligation-options-include (CloseEquityFinancingSale ?1 ?2) (next_event_td > (sectionEntrance_td + 5D)))
		)
	)

	(Action choose_cash_interest_repayment (TransitionsTo SRC)
		(AllowedSubjects Investor)
		(StateTransform
			(pay_interest_in_cash = true)
		)

	)

	(Action (CloseEquityFinancingSale
				(qualifying_round_price : $/Shares)
				(company_capitalization : PosShares)
		)
		(AllowedSubjects Company)
		(TransitionsTo Fulfilled)
		(StateTransform
			(local cap_price : $/Shares = (VALUATION_CAP / company_capitalization))
			(local discount_price : $/Shares = (qualifying_round_price * DISCOUNT_RATE)) ; check this!
			(local conversion_price : $/Shares = (min discount_price cap_price))
			(if pay_interest_in_cash (
				(local change : $ = (apply-macro interest_from_td event_td))
				(company_cash  -= change)
				(investor_cash += change)
				(investor_PreferredStock += (round (PURCHASE_AMOUNT / conversion_price)))
			) else (
				(local accrued_interest : $ = (apply-macro interest_from_td event_td))
				(investor_PreferredStock += (round ((PURCHASE_AMOUNT + accrued_interest) / conversion_price)))
			))

		)
	)


	(Action (CommitToAcquisition (company_capitalization : PosShares))
		; Option to either (a) convert to common stock at the cap or (b) get paid a multiple on the original investment.
		(AllowedSubjects Company)

		(FollowingSection
			(Next
				(Investor obligation-options-include ChooseMultipleRepayment discretionary)
				(Investor obligation-options-include (ChooseConvertToCommonStock company_capitalization) discretionary)
			)
		)
	)

	(Action ChooseMultipleRepayment
		(AllowedSubjects Investor)
		(StateTransform
			; from Corporate Transaction Payment defn
			(local change : $ = ((apply-macro interest_from_td event_td) + (2 * PURCHASE_AMOUNT)))
			; (company_cash -= change)
			(company_cash = (cast $ (company_cash - change)))
			(investor_cash += change)

		)
		(TransitionsTo Fulfilled)
	)

	(Action (ChooseConvertToCommonStock (company_capitalization : PosShares))
		(AllowedSubjects Investor)
		(StateTransform
			(local cap_price : Pos$ = (VALUATION_CAP / company_capitalization))
			(investor_Common_Stock += (round (PURCHASE_AMOUNT / cap_price)))
		)
		(TransitionsTo Fulfilled)
	)

	(Action ReachMaturity
		; Option to convert to Series Seed Preferred Stock at the cap. If debt version, option for repayment.
		(AllowedSubjects Env)

		(FollowingSection
			(Next
				(if DEBT_VERSION
					(Investor obligation-options-include (ChooseConversionToSeriesSeedPreferredStock ?1) discretionary)
				)
				(Investor obligation-options-include ChooseMaturityRepayment discretionary)

			)
		)
	)

	(Action ChooseMaturityRepayment
		(AllowedSubjects Investor)
		(StateTransform
			(local change : $ = ((apply-macro interest_from_td event_td) + PURCHASE_AMOUNT))
			; (company_cash -= change)
			(company_cash = (cast $ (company_cash - change)))
			(investor_cash += change)
		)
		(TransitionsTo Fulfilled)
	)

	(Action (ChooseConversionToSeriesSeedPreferredStock (company_capitalization : PosShares))
		(AllowedSubjects Investor)
		(StateTransform
			(local cap_price : Pos$ = (VALUATION_CAP / company_capitalization))
			(investor_SeriesSeedPreferredStock += (round (PURCHASE_AMOUNT / cap_price)))
		)
		(TransitionsTo Fulfilled)
	)

)




; PurchasePrice : #
; ConversionShares ... of CompanyPreferredStock
; ConversionSharesSect21 ... of a ShadowSeries

; Definition (a) of ConversionShares tells us there are 3 kinds of conversions:

; CapPrice (my term) := ValuationCap / ...