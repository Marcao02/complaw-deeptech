---------------------
file: serious/SAFE.l4

contract params:
    PURCHASE_AMOUNT : $ := 100000
    VALUATION_CAP : $ := 100
    START_INVESTOR_COMMON_STOCKS : ℕ := 0
    START_INVESTOR_PREFERRED_STOCKS : ℕ := 0
    START_INVESTOR_CASH : $ := 0

global vars:
    has_cap : Bool := VALUATION_CAP > 0
    writeAtMostOnce investor_remaining_investment_L : $
    writeAtMostOnce investor_Preferred_Stocks : ℕ := START_INVESTOR_PREFERRED_STOCKS
    writeAtMostOnce investor_Common_Stocks : ℕ := START_INVESTOR_COMMON_STOCKS
    writeAtMostOnce liq_price : $
    writeAtMostOnce liq_cashout : $ := 0
    writeAtMostOnce investor_cash : $ := START_INVESTOR_CASH
    writeAtMostOnce company_cash_remaining : ℕ
    writeAtMostOnce investor_percent_of_cashout_investor_investments : %
    writeAtMostOnce dis_cashout : $

section InvestorInvests:
    Company weakly-must CommitToEquityFinancing nodeadline
    Company weakly-must CommitToLiquidityEvent(false) nodeadline
    Company weakly-must CommitToLiquidityEvent(true) nodeadline
    Company weakly-must CommitToDissolution nodeadline

action CommitToEquityFinancing()  transitions to AfterCommitToEquityFinancing

section AfterCommitToEquityFinancing:
    Company weakly-must DeliverDocsWithPRA_E discretionary
    Company weakly-must DeliverDocsWithoutPRA_E discretionary

action DeliverDocsWithPRA_E()  transitions to AfterDeliverDocsWithPRA_E

section AfterDeliverDocsWithPRA_E:
    Company must IssueSAFEPreferredStock_E discretionary

action DeliverDocsWithoutPRA_E()  transitions to AfterDeliverDocsWithoutPRA_E

section AfterDeliverDocsWithoutPRA_E:
    Company must SubmitProRataRightsAgreementForSigning_E discretionary

action SubmitProRataRightsAgreementForSigning_E()  transitions to AfterSubmitProRataRightsAgreementForSigning_E

section AfterSubmitProRataRightsAgreementForSigning_E:
    Investor must SignPRA_E discretionary

action SignPRA_E()  transitions to AfterSignPRA_E

section AfterSignPRA_E:
    Company must IssueSAFEPreferredStock_E discretionary

action IssueSAFEPreferredStock_E(company_capitalization)  transitions to AfterIssueSAFEPreferredStock_E:
    transform:
        investor_SAFE_Preferred_Stocks += PURCHASE_AMOUNT / company_capitalization

section AfterIssueSAFEPreferredStock_E:
    Company must DoEquityFinancing discretionary

action DoEquityFinancing()  transitions to Fulfilled

action CommitToLiquidityEvent(change_of_control, _investor_percent_of_cashout_investor_investments, company_cash_at_liquidity_event, liquidity_capitalization, reduction_needed_to_qualify_as_usa_tax_free_reorg)  transitions to AfterCommitToLiquidityEvent:
    transform:
        liq_price := VALUATION_CAP / liquidity_capitalization
        investor_percent_of_cashout_investor_investments := _investor_percent_of_cashout_investor_investments
        if change_of_control and reduction_needed_to_qualify_as_usa_tax_free_reorg > 0:
            investor_Common_Stocks += (round _investor_percent_of_cashout_investor_investments * reduction_needed_to_qualify_as_usa_tax_free_reorg / liq_price)            investor_remaining_investment_L := PURCHASE_AMOUNT - _investor_percent_of_cashout_investor_investments * reduction_needed_to_qualify_as_usa_tax_free_reorg
        else:
            investor_remaining_investment_L := PURCHASE_AMOUNT
        company_cash_remaining := company_cash_at_liquidity_event - reduction_needed_to_qualify_as_usa_tax_free_reorg

section AfterCommitToLiquidityEvent:
    Investor may ChooseCashPayment_L discretionary
    Investor may ChooseStockPayment_L discretionary
    Company should AutoChooseStockPayment_L discretionary

action AutoChooseStockPayment_L()  transitions to AfterAutoChooseStockPayment_L

section AfterAutoChooseStockPayment_L:
    Company must TransferCommonStock_L discretionary

action ChooseStockPayment_L()  transitions to AfterChooseStockPayment_L

section AfterChooseStockPayment_L:
    Company must TransferCommonStock_L discretionary

action TransferCommonStock_L()  transitions to AfterTransferCommonStock_L:
    transform:
        investor_Common_Stocks += (round investor_remaining_investment_L - liq_cashout / liq_price)

section AfterTransferCommonStock_L:
    Company must DoLiquidityEvent discretionary

action ChooseCashPayment_L()  transitions to AfterChooseCashPayment_L

section AfterChooseCashPayment_L:
    Company must TransferCash_L discretionary

action TransferCash_L()  transitions to AfterTransferCash_L:
    transform:
        if investor_remaining_investment_L ≤ company_cash_remaining * investor_percent_of_cashout_investor_investments:
            liq_cashout := investor_remaining_investment_L
        else:
            liq_cashout := company_cash_remaining * investor_percent_of_cashout_investor_investments
        investor_cash += liq_cashout
        company_cash -= liq_cashout

section AfterTransferCash_L:
    if liq_cashout < investor_remaining_investment_L:
        Company must TransferCommonStock_L discretionary
    if liq_cashout == investor_remaining_investment_L:
        Company must DoLiquidityEvent discretionary

action DoLiquidityEvent()  transitions to Fulfilled

action CommitToDissolution()  transitions to AfterCommitToDissolution

section AfterCommitToDissolution:
    Company must TransferCash_D discretionary

action TransferCash_D(company_cash_at_dissolution_event, investor_percent_of_dissolving_investor_investments)  transitions to AfterTransferCash_D:
    transform:
        if PURCHASE_AMOUNT ≤ company_cash_at_dissolution_event * investor_percent_of_dissolving_investor_investments:
            dis_cashout := PURCHASE_AMOUNT
        else:
            dis_cashout := company_cash_at_dissolution_event * investor_percent_of_dissolving_investor_investments
        investor_cash := dis_cashout
        company_cash := company_cash_at_dissolution_event - dis_cashout

section AfterTransferCash_D:
    if dis_cashout < PURCHASE_AMOUNT:
        Company must TransferAssets_D discretionary
    if dis_cashout == PURCHASE_AMOUNT:
        Company must Dissolve discretionary

action TransferAssets_D(liquidity_capitalization)  transitions to AfterTransferAssets_D:
    transform:
        liq_price := VALUATION_CAP / liquidity_capitalization
        investor_Common_Stocks += PURCHASE_AMOUNT - dis_cashout / liq_price

section AfterTransferAssets_D:
    Company must Dissolve discretionary

action Dissolve()  transitions to Fulfilled
