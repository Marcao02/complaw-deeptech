(Actors Investor Company)
(TimeUnit d)

(TypeDefinitions
	($ := (Dimensioned NonnegReal "$"))
	(Pos$ := (Dimensioned PosReal "$"))

	(ShareCnt = (Dimensioned Nat "shares"))
	(PosShareCnt = (Dimensioned PosInt "shares"))

	(SharePrice = (Ratio $ PosShareCnt))
	(PosSharePrice = (Ratio Pos$ PosShareCnt))
)

(ContractParams
	(PURCHASE_AMOUNT : Pos$ = 1e4)
	(VALUATION_CAP : Pos$ = 1e6)
	(DISCOUNT_RATE : "Fraction(0,1]" = .95)
	(START_INVESTOR_COMMON_STOCKS : ShareCnt = 0)
	(START_INVESTOR_SAFE_PREFERRED_STOCKS : ShareCnt = 0)
	(START_INVESTOR_CASH : $ = 0)
)

(StateVars
	(investor_Common_Stocks : ShareCnt = START_INVESTOR_COMMON_STOCKS  )
	(investor_SAFE_Preferred_Stocks : ShareCnt = START_INVESTOR_SAFE_PREFERRED_STOCKS)
	(investor_cash : $ = START_INVESTOR_CASH )

	(cash_currently_unconverted : $ = PURCHASE_AMOUNT)

	; for Equity and Liquidity
	(conversion_price : SharePrice)
	(cap_price : SharePrice)
	(discount_price : SharePrice)
	(undiscounted_price_per_share_standard_preferred_stock : SharePrice)

	; for Liquidity
	(liq_cashout : $ = 0 )
	(investor_liq_hypothetical_shares : ShareCnt)
	(investor_percent_of_cashout_investor_investments : "Fraction[0,1)")
)

(Invariants
	(liq_cashout â‰¤ cash_currently_unconverted)
)


(Situations&Actions "safe_tutorial"
	(StartSituation InvestorInvests)

	(Situation InvestorInvests
		(Next
			(Company may CommitToEquityFinancing)
			(Company may (CommitToIPO ?1)) ; IPO
			; should Company breach after a while? no; company can actually remain in this state forever,
			; though they won't be able to raise any new money or sell.
		)
	)

	; ----------------- Equity Financing Event -------------------
	; NOTE: PRRA abbreviates pro rata rights agreement

	(Action CommitToEquityFinancing (AllowedSubjects Company)
		(FollowingSituation
			(Next
				(Company must SendEquityFinancingTransactionDocsWithPRRAForSigning (within 14d))
			)
		)
	)

	(Action SendEquityFinancingTransactionDocsWithPRRAForSigning (AllowedSubjects Company)
		(FollowingSituation
			(Next
				(Investor must ExecuteAndDeliverTransactionDocs (within 14d))
			)
		)
	)

	(Action ExecuteAndDeliverTransactionDocs (AllowedSubjects Investor)
		(FollowingSituation
			(Next
				(Company must (IssueSAFEPreferredStock ?1 ?2) (within 14d))
			)
		)
	)

	(Action (IssueSAFEPreferredStock
				(company_capitalization : PosShareCnt)
				(premoney_valuation : Pos$) ) (AllowedSubjects Company)
		(StateTransform
			(cap_price' = (VALUATION_CAP / company_capitalization))
			(undiscounted_price_per_share_standard_preferred_stock' = (premoney_valuation / company_capitalization))
			(discount_price' = (undiscounted_price_per_share_standard_preferred_stock' * DISCOUNT_RATE))
			(conversion_price' = (min cap_price' discount_price'))
			(investor_SAFE_Preferred_Stocks' += (PURCHASE_AMOUNT round/ conversion_price'))
		)
		(FollowingSituation
			(Next (Company must DoEquityFinancing (within 1d)))
		)
	)

	(Action DoEquityFinancing (AllowedSubjects Company) (TransitionsTo Fulfilled))


	; ----------------- Liquidity Event -------------------

	(Action (CommitToIPO (liquidity_capitalization:PosShareCnt)
			) (AllowedSubjects Company)
		(StateTransform
			(cap_price' = (VALUATION_CAP / liquidity_capitalization))
			(investor_liq_hypothetical_shares' = (PURCHASE_AMOUNT round/ cap_price'))
			(investor_percent_of_cashout_investor_investments' = (fraction-of-sum investor_liq_hypothetical_shares' liquidity_capitalization))
			(cash_currently_unconverted' = PURCHASE_AMOUNT)
		)

		(FollowingSituation
			(Next
				; 1W is totally made up by Dustin!
				(Investor may ChooseCashPayment [when next_event_td < (last_situation_td + 1W)])
				(Investor may ChooseStockPayment [when next_event_td < (last_situation_td + 1W)])
				(Company may AutoChooseStockPayment [when next_event_td == (last_situation_td + 1W)])
				(Breach_Company [after last_situation_td + 1W])
			)
		)
	)

	(Action AutoChooseStockPayment (AllowedSubjects Company)
		(FollowingSituation (Next (Company must TransferCommonStock (within 14d))))
	)

	(Action ChooseStockPayment (AllowedSubjects Investor)
		(FollowingSituation (Next (Company must TransferCommonStock (within 14d))))
	)

	(Action TransferCommonStock (AllowedSubjects Company)
		(StateTransform
			(investor_Common_Stocks' += ((check $ (cash_currently_unconverted - liq_cashout)) round/ cap_price))
		)
		(FollowingSituation
			(Next (Company must DoLiquidityEvent (within 14d)))
		)
	)

	(Action ChooseCashPayment (AllowedSubjects Investor)
		(FollowingSituation
			(Next (Company must (TransferCash_L ?1) (within 14d)))
		)
	)

	(Action (TransferCash_L (company_cash_at_liquidity_event : Pos$)) (AllowedSubjects Company)
		(StateTransform
			(liq_cashout' = (min
				cash_currently_unconverted
				(company_cash_at_liquidity_event * investor_percent_of_cashout_investor_investments)
			))
			(investor_cash' += liq_cashout')
		)

		(FollowingSituation
			(Next
				(if (liq_cashout < cash_currently_unconverted)
					(Company must TransferCommonStock (within 14d)) )

				(if (liq_cashout == cash_currently_unconverted)
					(Company must DoLiquidityEvent (within 14d))
				)
			)
		)
	)

	(Action DoLiquidityEvent (AllowedSubjects Company) (TransitionsTo Fulfilled))

)