
(Roles Buyer Seller)

(TimeUnit d)

(SortDefinitions
	(Pos$ := (Dup PosReal "Pos$"))
	($ := (Dup NonnegReal "$"))
)


(ContractParams
	(INTEREST_RATE : PosReal = .1)
	(TOTAL_DUE_BEFORE_CLOSING : Pos$ = (Pos$ 9500))
	(MIN_PAYMENT : Pos$ = (Pos$ 500))
	(DUE_AT_CLOSING : Pos$ = (Pos$ 500))
)

(GlobalVars
	(balance_remaining : $ = TOTAL_DUE_BEFORE_CLOSING)
	(next_payment_month : Nat = 0)  )

(ProseContract
	(P1 'Buyer agrees to pay to Seller the total sum e10000, in the manner following:')
	(P2 'e500 is to be paid at closing, and the remaining balance of e9500 shall be paid as follows:')
	(P3 'e500 or more per month on the first day of each and every month, and continuing until the entire balance, including both principal and interest, shall be paid in full -- provided, however, that the entire balance due plus accrued interest and any other amounts due here-under shall be paid in full on or before 24 months.')
	(P4 'Monthly payments shall include both principal and interest with interest at the rate of 10%, computed monthly on the remaining balance from time to time unpaid.')  )

(FormalContract
	"Instalment Sale Hvitved"
	(StartSection WaitingForFirstDayOfNextMonth)

	(Section WaitingForFirstDayOfNextMonth
		(Next
			; note contract says payments happen on the first day of the month, not on-or-before.
			; so I am being lazy and interpreting it literally.

			; Can make a non-final payment, provided it's at least MIN_PAYMENT
			; and it's before the final month of the contract.
			(if ( (MIN_PAYMENT ≤ balance_remaining) and (next_payment_month < 23) )
				(Buyer obligation-options-include (PayInstallment ?amount)
					(next_event_td == (sectionEntrance_td + 30d))
					(where ((MIN_PAYMENT ≤ ?amount ) and (?amount ≤ balance_remaining))) ))

			(Buyer obligation-options-include (PayLastInstallment (balance_remaining + DUE_AT_CLOSING))
				(next_event_td == (sectionEntrance_td + 30d))
			)
		))

	(Action (PayInstallment(amount:Pos$)) (TransitionsTo WaitingForFirstDayOfNextMonth)
		(StateTransform ; conjecture statement is unnecessary
			(conjecture (amount ≤ balance_remaining)) ; overpaying is forbidden
			(balance_remaining := (cast $ (balance_remaining - amount)))
			(balance_remaining *= (1 + INTEREST_RATE))
			(next_payment_month += 1)   ))


	(Action (PayLastInstallment(amount:Pos$)) (TransitionsTo Fulfilled)
		(StateTransform ; neither of these statements is necessary
			(conjecture (amount == (balance_remaining + DUE_AT_CLOSING)))
			(balance_remaining = ($ 0))    )))