(Roles Buyer Seller)
(TimeUnit d)
(ProseContract
	(Condition1 "The option {Buyer} gives the option {Seller} $5 on June 30, 2015 to buy an American call option consisting of a conditional agreement composed of the following two components.")
	(Condition2 "The option {Buyer} chooses to exercise the option by paying $80 to option {Seller} on or before December 17, 2015.")
	(Agreement "The option {Seller} is obligated to transfer one share of stock to the option {Buyer} no later than 30 days after the option is exercised.")
)

(ContractParams
	(SELLER_FUNDS : $ = 0)
	(BUYER_FUNDS : $ = 85)
	(BUYER_START_SHARES : ShareCnt = 0)
	(SELLER_START_SHARES : PosShareCnt = 1)
)

(SortDefinitions
	($ := (Dimensioned NonnegReal "$"))
	(Pos$ := (Dimensioned PosReal "$"))
	(ShareCnt = (Dimensioned Nat "shares"))
	(PosShareCnt = (Dimensioned PosInt "shares"))
)

(GlobalVars
	(writeonce seller_funds : $ = SELLER_FUNDS)
	(writeonce buyer_funds : $ = BUYER_FUNDS)
	(writeAtMostOnce buyer_final_shares : ShareCnt)
	(writeAtMostOnce seller_final_shares : ShareCnt)
)

(FormalContract
	"Farmer American call option from A Formal Language for Writing Contracts 2016"

	(StartSection BeforeOptionPurchase)

	(Section BeforeOptionPurchase
		(Next
			(Buyer must PaySellerForOption (next_event_dt == (str2datetime "June 30, 2015")))
		)
	)

	(Action PaySellerForOption
		(StateTransform
			(buyer_funds' = (cast $ (buyer_funds - ($ 5))))
			(seller_funds' += ($ 5))
		)

		(FollowingSection
			(Next
				(Buyer may ExerciseOption (next_event_dt ≤ (str2datetime "December 17, 2015")))
			)
		)
	)

	(Action ExerciseOption
		(StateTransform
			(buyer_funds' = (cast $ (buyer_funds - ($ 80))))
			(seller_funds' += ($ 80))
		)

		(FollowingSection
			(Next
				(Buyer must TransferShare (next_event_td ≤ (event_td + 30D)))
			)
		)
	)

	(Action TransferShare
		(StateTransform
			(buyer_final_shares' = (BUYER_START_SHARES + (ShareCnt 1)))
			(seller_final_shares' = (cast ShareCnt (SELLER_START_SHARES - (ShareCnt 1))))
		)
		(TransitionsTo Fulfilled)
	)
)
