--------------------------------------
file: serious/SAFE_2_liq_eventtypes.l4

roles: Env, Investor, Company

contract params:
    PURCHASE_AMOUNT : Pos$ := 100000
    VALUATION_CAP : Pos$ := 1000000000000
    DISCOUNT_RATE : Fraction(0,1] := 1
    START_INVESTOR_COMMON_STOCKS : ShareCnt := 0
    START_INVESTOR_SAFE_PREFERRED_STOCKS : ShareCnt := 0
    START_INVESTOR_CASH : $ := 0

global vars:
    has_cap : Bool := (VALUATION_CAP < (cast Pos$ 1000000000000))
    has_discount : Bool := (DISCOUNT_RATE < 1)
    writeatmostonce investor_Common_Stocks : ShareCnt := START_INVESTOR_COMMON_STOCKS
    writeatmostonce investor_SAFE_Preferred_Stocks : ShareCnt := START_INVESTOR_SAFE_PREFERRED_STOCKS
    writeatmostonce investor_cash : $ := START_INVESTOR_CASH
    cash_currently_unconverted : $ := PURCHASE_AMOUNT
    writeatmostonce safe_price : SharePrice
    writeatmostonce discount_price : SharePrice
    writeatmostonce conversion_price : SharePrice
    writeatmostonce initial_price_per_share_standard_preferred_stock : SharePrice
    writeatmostonce liq_price : SharePrice
    writeatmostonce liq_cashout : $ := 0
    writeatmostonce company_cash : $
    writeatmostonce investor_liq_hypothetical_shares : ShareCnt
    writeatmostonce investor_percent_of_cashout_investor_investments : Fraction[0,1)
    writeatmostonce dis_cashout : $

section InvestorInvests:
    Company obligation-options-include CommitToEquityFinancing no_time_constraint
    Company obligation-options-include CommitToLiquidityEvent(?1, ?2) no_time_constraint
    Company obligation-options-include CommitToLiquidityEventWithCoCTaxFreeReorg(?1, ?2, ?3) no_time_constraint
    Company obligation-options-include CommitToDissolution no_time_constraint

action CommitToEquityFinancing()  transitions to AfterCommitToEquityFinancing
    following section:
        Company obligation-options-include DeliverDocsWithPRA discretionary
        Company obligation-options-include DeliverDocsWithoutPRA discretionary

action DeliverDocsWithPRA()  transitions to AfterDeliverDocsWithPRA
    following section:
        Company must IssueSAFEPreferredStock(?1, ?2) discretionary

action DeliverDocsWithoutPRA()  transitions to AfterDeliverDocsWithoutPRA
    following section:
        Company must SubmitProRataRightsAgreementForSigning discretionary

action SubmitProRataRightsAgreementForSigning()  transitions to AfterSubmitProRataRightsAgreementForSigning
    following section:
        Investor must SignPRA discretionary

action SignPRA()  transitions to AfterSignPRA
    following section:
        Company must IssueSAFEPreferredStock(?1, ?2) discretionary

action IssueSAFEPreferredStock(company_capitalization: PosShareCnt, premoney_valuation: Pos$) transitions to AfterIssueSAFEPreferredStock:
    transform:
        safe_price := (VALUATION_CAP / company_capitalization)
        initial_price_per_share_standard_preferred_stock := (premoney_valuation / company_capitalization)
        discount_price := (initial_price_per_share_standard_preferred_stock * DISCOUNT_RATE)
        conversion_price := (min safe_price discount_price)
        investor_SAFE_Preferred_Stocks := (investor_SAFE_Preferred_Stocks + (PURCHASE_AMOUNT round/ conversion_price))
    following section:
        Company must DoEquityFinancing discretionary

action DoEquityFinancing()  transitions to Fulfilled

action CommitToLiquidityEvent(company_cash_at_liquidity_event: $, liquidity_capitalization: PosShareCnt) transitions to AfterCommitToLiquidityEvent:
    transform:
        liq_price := (VALUATION_CAP / liquidity_capitalization)
        investor_liq_hypothetical_shares := (PURCHASE_AMOUNT round/ liq_price)
        investor_percent_of_cashout_investor_investments := (fraction-of-sum investor_liq_hypothetical_shares liquidity_capitalization)
        cash_currently_unconverted := PURCHASE_AMOUNT
        company_cash := company_cash_at_liquidity_event
    following section:
        Investor may ChooseCashPayment discretionary
        Investor may ChooseStockPayment discretionary
        Company should AutoChooseStockPayment discretionary

action CommitToLiquidityEventWithCoCTaxFreeReorg(company_cash_at_liquidity_event: $, liquidity_capitalization: PosShareCnt, reduction_needed_to_qualify_as_usa_tax_free_reorg: $) transitions to AfterCommitToLiquidityEventWithCoCTaxFreeReorg:
    transform:
        liq_price := (VALUATION_CAP / liquidity_capitalization)
        investor_liq_hypothetical_shares := (PURCHASE_AMOUNT round/ liq_price)
        investor_percent_of_cashout_investor_investments := (fraction-of-sum investor_liq_hypothetical_shares liquidity_capitalization)
        purchase_amount_reduction : $ := (investor_percent_of_cashout_investor_investments * reduction_needed_to_qualify_as_usa_tax_free_reorg)
        investor_Common_Stocks := (investor_Common_Stocks + (purchase_amount_reduction round/ liq_price))
        cash_currently_unconverted := (cast $ (PURCHASE_AMOUNT - purchase_amount_reduction))
        company_cash := (cast $ (company_cash_at_liquidity_event - reduction_needed_to_qualify_as_usa_tax_free_reorg))
    following section:
        Investor may ChooseCashPayment (next_event_td < (sectionEntrance_td + 1w))
        Investor may ChooseStockPayment (next_event_td < (sectionEntrance_td + 1w))
        Company obligation-options-include AutoChooseStockPayment (next_event_td == (sectionEntrance_td + 1w))

action AutoChooseStockPayment()  transitions to AfterAutoChooseStockPayment
    following section:
        Company must TransferCommonStock discretionary

action ChooseStockPayment()  transitions to AfterChooseStockPayment
    following section:
        Company must TransferCommonStock discretionary

action TransferCommonStock()  transitions to AfterTransferCommonStock:
    transform:
        investor_Common_Stocks := (investor_Common_Stocks + ((cast $ (cash_currently_unconverted - liq_cashout)) round/ liq_price))
    following section:
        Company must DoLiquidityEvent discretionary

action ChooseCashPayment()  transitions to AfterChooseCashPayment
    following section:
        Company must TransferCash_L discretionary

action TransferCash_L()  transitions to AfterTransferCash_L:
    transform:
        liq_cashout := (min cash_currently_unconverted (company_cash * investor_percent_of_cashout_investor_investments))
        investor_cash := (investor_cash + liq_cashout)
    following section:
        if (liq_cashout < cash_currently_unconverted):
            Company must TransferCommonStock discretionary
        if (liq_cashout == cash_currently_unconverted):
            Company must DoLiquidityEvent discretionary

action DoLiquidityEvent()  transitions to Fulfilled

action CommitToDissolution()  transitions to AfterCommitToDissolution
    following section:
        Company must TransferCash_D(?company_cash_at_dissolution_event, ?investor_percent_of_dissolving_investor_investments) discretionary

action TransferCash_D(company_cash_at_dissolution_event: $, investor_percent_of_dissolving_investor_investments: Fraction(0,1]) transitions to AfterTransferCash_D:
    transform:
        dis_cashout := (min PURCHASE_AMOUNT (company_cash_at_dissolution_event * investor_percent_of_dissolving_investor_investments))
        investor_cash := dis_cashout
    following section:
        if (dis_cashout < PURCHASE_AMOUNT):
            Company must TransferAssets(?1) discretionary
        if (dis_cashout == PURCHASE_AMOUNT):
            Company must Dissolve discretionary

action TransferAssets(liquidity_capitalization: PosShareCnt) transitions to AfterTransferAssets:
    transform:
        liq_price := (VALUATION_CAP / liquidity_capitalization)
        investor_Common_Stocks := (investor_Common_Stocks + ((cast $ (PURCHASE_AMOUNT - dis_cashout)) round/ liq_price))
    following section:
        Company must Dissolve discretionary

action Dissolve()  transitions to Fulfilled
