-----------------------------------------------------
file: toy_and_teaching/monster_burger_program_only.l4

roles: Env, Challenger, Restaurant

contract params:
    MB_PRICE : $ := 50
    TIME_TO_EAT_BURGER : TimeDelta := 1h
    MAX_SERVE_DELAY : TimeDelta := 20m
    MAX_CLAIM_VERIFICATION_DELAY : TimeDelta := 10m
    MAX_REFUND_DELAY : TimeDelta := 10m

global vars:
    writeonce challenge_endlimit_timestamp : TimeDelta
    amount_owing : $ := 0
    amount_paid : $ := 0

situation MonsterBurgerUncooked:
    Challenger must RequestCookMB (next_event_td ≤ (situationEntrance_td + 30m))

action RequestCookMB()  transitions to AfterRequestCookMB:
    transform:
        amount_owing := MB_PRICE
    following situation:
        Restaurant should ServeMB (next_event_td < (MAX_SERVE_DELAY + situationEntrance_td))
        EnterPromptServeGuaranteeViolated (next_event_td ≥ (MAX_SERVE_DELAY + situationEntrance_td))

action ServeMB()  transitions to EatingMB:
    transform:
        challenge_endlimit_timestamp := (TIME_TO_EAT_BURGER + event_td)

action EnterPromptServeGuaranteeViolated()  transitions to Fulfilled:
    transform:
        amount_owing := 0

action EnterEatingMB()  transitions to EatingMB

situation EatingMB:
    Challenger may AnnounceMBFinished (next_event_td < challenge_endlimit_timestamp)
    Restaurant should CheckCompletionClaim (and (challenge_endlimit_timestamp ≤ next_event_td) (next_event_td ≤ (MAX_CLAIM_VERIFICATION_DELAY + situationEntrance_td)))
    TimeToCheckCompletionExpires (next_event_td == (MAX_CLAIM_VERIFICATION_DELAY + challenge_endlimit_timestamp))

action AnnounceMBFinished()  transitions to AfterAnnounceMBFinished
    following situation:
        Restaurant should CheckCompletionClaim (next_event_td ≤ (situationEntrance_td + MAX_CLAIM_VERIFICATION_DELAY))
        TimeToCheckCompletionExpires (next_event_td == (MAX_CLAIM_VERIFICATION_DELAY + situationEntrance_td))

action TimeToCheckCompletionExpires()  transitions to Fulfilled:
    transform:
        amount_owing := 0

action CheckCompletionClaim()  transitions to AfterCheckCompletionClaim
    following situation:
        Restaurant obligation-options-include VerifyCompletionClaim immediately
        Restaurant obligation-options-include RejectCompletionClaim immediately

action RejectCompletionClaim()  transitions to AfterRejectCompletionClaim
    following situation:
        if (situationEntrance_td ≤ challenge_endlimit_timestamp):
            EnterEatingMB
        if (situationEntrance_td > challenge_endlimit_timestamp):
            Challenger must PayForMB immediately

action PayForMB()  transitions to Fulfilled:
    transform:
        amount_paid := amount_owing
        amount_owing := 0

action VerifyCompletionClaim()  transitions to Fulfilled:
    transform:
        amount_owing := 0
