-----------------------------------------------------
file: toy_and_teaching/monster_burger_program_only.l4

roles: Env, Arbiter, Challenger, Restaurant

contract params:
    MB_PRICE : $ := 50
    TIME_TO_EAT_BURGER : TimeDelta := 1h
    MAX_SERVE_DELAY : TimeDelta := 20m
    MAX_CLAIM_VERIFICATION_DELAY : TimeDelta := 10m
    MAX_REFUND_DELAY : TimeDelta := 10m

state vars:
    writeonce challenge_endlimit_timestamp : TimeDelta
    amount_owing : $ := 0
    amount_paid : $ := 0

situation MonsterBurgerUncooked:
    Challenger must RequestCookMB (next_event_td ≤ (situation_entrance_td + 30m))

action RequestCookMB()  transitions to AfterRequestCookMB:
    transform:
        amount_owing := MB_PRICE
    following situation:
        EnterPromptServeGuaranteeViolated (next_event_td ≥ (MAX_SERVE_DELAY + situation_entrance_td))
        Restaurant may ServeMB (next_event_td < (MAX_SERVE_DELAY + situation_entrance_td))

action ServeMB()  transitions to EatingMB:
    transform:
        challenge_endlimit_timestamp := (TIME_TO_EAT_BURGER + event_td)

action EnterPromptServeGuaranteeViolated()  transitions to Fulfilled:
    transform:
        amount_owing := 0

action EnterEatingMB()  transitions to EatingMB

situation EatingMB:
    Challenger may AnnounceMBFinished (next_event_td < challenge_endlimit_timestamp)
    TimeToCheckCompletionExpires (next_event_td == (MAX_CLAIM_VERIFICATION_DELAY + challenge_endlimit_timestamp))
    Restaurant may CheckCompletionClaim (and (challenge_endlimit_timestamp ≤ next_event_td) (next_event_td ≤ (MAX_CLAIM_VERIFICATION_DELAY + situation_entrance_td)))

action AnnounceMBFinished()  transitions to AfterAnnounceMBFinished
    following situation:
        TimeToCheckCompletionExpires (next_event_td == (MAX_CLAIM_VERIFICATION_DELAY + situation_entrance_td))
        Restaurant may CheckCompletionClaim (next_event_td ≤ (situation_entrance_td + MAX_CLAIM_VERIFICATION_DELAY))

action TimeToCheckCompletionExpires()  transitions to Fulfilled:
    transform:
        amount_owing := 0

action CheckCompletionClaim()  transitions to AfterCheckCompletionClaim
    following situation:
        Breach_Restaurant (next_event_td == (situation_entrance_td + 1m))
        Restaurant may RejectCompletionClaim immediately
        Restaurant may VerifyCompletionClaim immediately

action RejectCompletionClaim()  transitions to AfterRejectCompletionClaim
    following situation:
        if (situation_entrance_td > challenge_endlimit_timestamp):
            Challenger must PayForMB immediately
        if (situation_entrance_td ≤ challenge_endlimit_timestamp):
            EnterEatingMB

action PayForMB()  transitions to Fulfilled:
    transform:
        amount_paid := amount_owing
        amount_owing := 0

action VerifyCompletionClaim()  transitions to Fulfilled:
    transform:
        amount_owing := 0
