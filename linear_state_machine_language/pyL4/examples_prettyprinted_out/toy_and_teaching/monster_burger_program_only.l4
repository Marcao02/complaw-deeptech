-----------------------------------------------------
file: toy_and_teaching/monster_burger_program_only.l4

roles: Env, Challenger, Restaurant

contract params:
    MB_PRICE : Price := 50
    TIME_TO_EAT_BURGER : Duration := 1h
    MAX_SERVE_DELAY : Duration := 20m
    MAX_CLAIM_VERIFICATION_DELAY : Duration := 10m
    MAX_REFUND_DELAY : Duration := 10m

global vars:
    writeonce challenge_endlimit_timestamp : Timestamp
    amount_owing : Price := 0
    amount_paid : Price := 0

section MonsterBurgerUncooked:
    Challenger must RequestCookMB event_td ≤ sectionEntrance_td + 30m

action RequestCookMB()  transitions to AfterRequestCookMB:
    transform:
        amount_owing := MB_PRICE
    following section:
        Restaurant should ServeMB event_td < MAX_SERVE_DELAY + sectionEntrance_td
        EnterPromptServeGuaranteeViolated event_td ≥ MAX_SERVE_DELAY + sectionEntrance_td

action ServeMB()  transitions to EatingMB:
    transform:
        challenge_endlimit_timestamp := TIME_TO_EAT_BURGER unitsAfter sectionEntrance_td

action EnterPromptServeGuaranteeViolated()  transitions to Fulfilled:
    transform:
        amount_owing := 0

action EnterEatingMB()  transitions to EatingMB

section EatingMB:
    Challenger may AnnounceMBFinished event_td < challenge_endlimit_timestamp
    Restaurant should CheckCompletionClaim challenge_endlimit_timestamp ≤ event_td and event_td ≤ MAX_CLAIM_VERIFICATION_DELAY + sectionEntrance_td
    TimeToCheckCompletionExpires (== event_td MAX_CLAIM_VERIFICATION_DELAY unitsAfter challenge_endlimit_timestamp)

action AnnounceMBFinished()  transitions to AfterAnnounceMBFinished
    following section:
        Restaurant should CheckCompletionClaim event_td ≤ sectionEntrance_td + MAX_CLAIM_VERIFICATION_DELAY
        TimeToCheckCompletionExpires (== event_td MAX_CLAIM_VERIFICATION_DELAY unitsAfter sectionEntrance_td)

action TimeToCheckCompletionExpires()  transitions to Fulfilled:
    transform:
        amount_owing := 0

action CheckCompletionClaim()  transitions to AfterCheckCompletionClaim
    following section:
        Restaurant weakly-must VerifyCompletionClaim immediately
        Restaurant weakly-must RejectCompletionClaim immediately

action RejectCompletionClaim()  transitions to AfterRejectCompletionClaim
    following section:
        if sectionEntrance_td ≤ challenge_endlimit_timestamp:
            EnterEatingMB
        if sectionEntrance_td > challenge_endlimit_timestamp:
            Challenger must PayForMB immediately

action PayForMB()  transitions to Fulfilled:
    transform:
        amount_paid := amount_owing
        amount_owing := 0

action VerifyCompletionClaim()  transitions to Fulfilled:
    transform:
        amount_owing := 0
