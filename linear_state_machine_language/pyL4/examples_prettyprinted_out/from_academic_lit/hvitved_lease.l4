----------------------------------------
file: from_academic_lit/hvitved_lease.l4

roles: Env, Landlord, Tenant

contract params:
    LEASE_DURATION : TimeUnit := (days 4 * 29)
    RENT : Money := 1000
    MAX_RENT_DELAY : TimeUnit := 7d
    MIN_TERMINATION_NOTICE : TimeUnit := 30d

global vars:
    lease_term_start : ℕ
    lease_term_end : ℕ
    writeOnceMore lease_terminated : Bool := False
    branchUnaffecting inconly total_rent_paid : ℕ := 0

section Start:
    Landlord must EnsureApartmentReady immediately

action RequestTerminationFromMonthStarted()  transitions to MonthStarted:
    transform:
        lease_terminated := True

action RequestTerminationFromAfterPayRent()  transitions to AfterPayRent:
    transform:
        lease_terminated := True

action RequestTerminationFromMonthEnded()  transitions to MonthEnded:
    transform:
        lease_terminated := True

action EnsureApartmentReady()  transitions to AfterEnsureApartmentReady

section AfterEnsureApartmentReady:
    StartLeaseTerm

action StartLeaseTerm()  transitions to MonthStarted:
    transform:
        lease_term_start := event_td
        lease_term_end := event_td + LEASE_DURATION

action EnterMonthStarted()  transitions to MonthStarted

section MonthStarted:
    Tenant weakly-must PayRent event_td ≤ (monthStartDay_td ) + MAX_RENT_DELAY
    if ¬lease_terminated:
        Tenant may RequestTerminationFromMonthStarted event_td ≤ (earliest (monthStartDay_td ) + MAX_RENT_DELAY lease_term_end - MIN_TERMINATION_NOTICE)
    if ¬lease_terminated:
        Landlord may RequestTerminationFromMonthStarted event_td ≤ (earliest (monthStartDay_td ) + MAX_RENT_DELAY lease_term_end - MIN_TERMINATION_NOTICE)

action PayRent()  transitions to AfterPayRent:
    transform:
        total_rent_paid += 1000
    following section:
        EnterMonthEnded (== event_td (monthEndDay_td ))
        if ¬lease_terminated:
            Tenant may RequestTerminationFromAfterPayRent event_td ≤ (earliest (monthStartDay_td ) + MAX_RENT_DELAY lease_term_end - MIN_TERMINATION_NOTICE)
        if ¬lease_terminated:
            Landlord may RequestTerminationFromAfterPayRent event_td ≤ (earliest (monthStartDay_td ) + MAX_RENT_DELAY lease_term_end - MIN_TERMINATION_NOTICE)

action EnterMonthEnded()  transitions to MonthEnded
    following section:
        pre: sectionEntrance_td ≤ lease_term_end
        if lease_term_end ≤ sectionEntrance_td + 1d:
            EnterLeaseTermEnded
        if lease_term_end > sectionEntrance_td + 1d:
            EnterMonthStarted (== sectionEntrance_td + 1d event_td)
        if ¬lease_terminated:
            Tenant may RequestTerminationFromMonthEnded event_td ≤ (earliest (monthStartDay_td ) + MAX_RENT_DELAY lease_term_end - MIN_TERMINATION_NOTICE)
        if ¬lease_terminated:
            Landlord may RequestTerminationFromMonthEnded event_td ≤ (earliest (monthStartDay_td ) + MAX_RENT_DELAY lease_term_end - MIN_TERMINATION_NOTICE)

action EnterLeaseTermEnded()  transitions to LeaseTermEnded
    following section:
        if lease_terminated:
            Tenant must MoveOut immediately
        if ¬lease_terminated:
            StartLeaseTerm

action MoveOut()  transitions to Fulfilled
