-------------------------
file: serious/wip/KISS.l4

roles: Env, Investor, Company

contract params:
    PURCHASE_AMOUNT : $ := 100000
    TIME_TILL_MATURITY : TimeDelta := (days 365 + 364 / 2)
    VALUATION_CAP : $ := inf
    DISCOUNT_RATE : real01 := 1
    INTEREST_RATE : real01 := 0.04
    DEBT_VERSION : Bool := True
    START_INVESTOR_COMMON_STOCK : shares := 0
    START_INVESTOR_PREFERRED_STOCK : shares := 0
    START_INVESTOR_CASH : $ := 0

global vars:
    investor_cash : $ := START_INVESTOR_CASH
    investor_Common_Stock : shares := START_INVESTOR_COMMON_STOCK
    investor_SeriesSeedPreferredStock : shares := START_INVESTOR_PREFERRED_STOCK

section InvestorInvests:
    Company obligation-options-include CommitToAcquisition(_1) no_time_constraint
    Company obligation-options-include CommitToFinancing(_1, _2, _3) no_time_constraint
    Company obligation-options-include ReachMaturity event_td ≥ sectionEntrance_td + TIME_TILL_MATURITY

action CommitToFinancing(qualifying_round_new_money, qualifying_round_price, company_capitalization)  transitions to AfterCommitToFinancing:
    pre: qualifying_round_new_money ≥ 1000000
    transform:
        cap_price : $/share := VALUATION_CAP / company_capitalization
        discount_price : $/share := qualifying_round_price * DISCOUNT_RATE
        conversion_price : $/share := (min discount_price cap_price)

action CommitToAcquisition(company_capitalization)  transitions to AfterCommitToAcquisition
    following section:
        Investor obligation-options-include ChooseMultipleRepayment discretionary
        Investor obligation-options-include ChooseConvertToCommonStock(company_capitalization) discretionary

action ChooseMultipleRepayment()  transitions to Fulfilled:
    transform:
        first_year_interest : $ := INTEREST_RATE * PURCHASE_AMOUNT
        eighteen_month_interest : $ := first_year_interest + PURCHASE_AMOUNT + first_year_interest * INTEREST_RATE
        investor_cash += eighteen_month_interest + 2 * PURCHASE_AMOUNT

action ChooseConvertToCommonStock(company_capitalization)  transitions to Fulfilled:
    transform:
        cap_price : $ := VALUATION_CAP / company_capitalization
        investor_Common_Stock += (round PURCHASE_AMOUNT / cap_price)

action ReachMaturity()  transitions to AfterReachMaturity
    following section:
        if DEBT_VERSION:
            Investor obligation-options-include ChooseConversionToSeriesSeedPreferredStock(_1) discretionary
        Investor obligation-options-include ChooseMaturityRepayment discretionary

action ChooseMaturityRepayment()  transitions to Fulfilled:
    transform:
        first_year_interest : $ := INTEREST_RATE * PURCHASE_AMOUNT
        eighteen_month_interest : $ := first_year_interest + PURCHASE_AMOUNT + first_year_interest * INTEREST_RATE
        investor_cash += PURCHASE_AMOUNT + eighteen_month_interest

action ChooseConversionToSeriesSeedPreferredStock(company_capitalization)  transitions to Fulfilled:
    transform:
        cap_price : $ := VALUATION_CAP / company_capitalization
        investor_SeriesSeedPreferredStock += (round PURCHASE_AMOUNT / cap_price)
