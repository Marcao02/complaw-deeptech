
(Claims
	(MAX_TIME' = 44D)
	(MAX_SITUATION_VISITS' = 6)
)

(Roles Buyer Seller)

(TimeUnit d)

(ContractParams
	(CONTRACT_START : TimeDelta := 0D)
	(DELIVERY_TIME_ALLOWANCE : TimeDelta := 10D)
	; (CONTRACT_START : Date := "2010-12-30")
	; (DELIVERY_DEADlINE : Date := "2011-01-01")
)


(StateVars
	(nonoperative buyer_paid : Nat := 0)
	(nonoperative seller_refunded : Nat := 0)
	(writes≤1 delivery_td : TimeDelta)
)

(ProseContract
	(P1 'Seller agrees to transfer and deliver to Buyer, on or before {DELIVERY_TIME_ALLOWANCE}, the goods: 1 laser printer.')
	(P2 'Buyer agrees to accept the goods and to pay a total of €200 for them according to the terms further set out below.')
	(P3 'Paragraph 3. Buyer agrees to pay for the goods half upon receipt, with the remainder due within 30 days of delivery.')
	(P4 'If Buyer fails to pay the second half within 30 days, an additional fine of 10% has to be paid within 14 days.')
	(P5	'Upon receipt, Buyer has 14 days to return the goods to Seller in original, unopened packaging. Within 7 days thereafter, Seller has to repay the total amount to Buyer.')
)

(FormalContract
	"Two part sale Hvitved Laser Printer"
	(StartSituation Awaiting_Delivery)

	(Situation Awaiting_Delivery
		[ProseRefs P1]
		(Seller must Deliver (within DELIVERY_TIME_ALLOWANCE) )
		; SHOULD translate to:
		; (Breach_Seller (next_event_td > DELIVERY_DEADLINE))
    	; (Seller may Deliver (next_event_td ≤ DELIVERY_DEADLINE))
	)

	(Action Deliver [ProseRefs P1]
		(StateTransform
			(delivery_td' = last_event_td)
		)
		(FollowingSituation
			(Buyer must Pay1stInstallment immediately)
		)
	)

	(Action Pay1stInstallment [ProseRefs P1 P2 P3]
		(StateTransform
			(prove (buyer_paid == 0))
			(buyer_paid' = 100)
		)
		(FollowingSituation
			(Buyer may Pay2ndInstallment (when next_event_td ≤ (30d + delivery_td)))
			(Buyer may ReturnOnTime (when next_event_td ≤ (14d + delivery_td)))
			(EnterLate2ndInstallment (when next_event_td > (30d + delivery_td)))
		)
	)

	(Action EnterLate2ndInstallment [ProseRefs P4]
		(FollowingSituation
			(Buyer must PayLateFee (within (44d + delivery_td)))
		)
	)

	(Action Pay2ndInstallment [ProseRefs P5]
		(StateTransform
			(prove (buyer_paid == 100))
			(buyer_paid' = 200)
		)
		(FollowingSituation
			(Buyer may ReturnOnTime (when next_event_td ≤ (14d + delivery_td)))
			(CompleteNormalSale (when next_event_td > (14d + delivery_td)))
		)
	)

	(Action ReturnOnTime [ProseRefs P5]
		(FollowingSituation
			(Seller must DeliverRefund (within (7d + last_situation_td)))
		)
	)

	(Action PayLateFee [ProseRefs P4]
		(StateTransform
			(prove (buyer_paid == 100))
			(buyer_paid' = 220)
		)
		(TransitionsTo Fulfilled)
	)

	(Action CompleteNormalSale [ProseRefs P2]
		 (TransitionsTo Fulfilled)
	)

	(Action DeliverRefund [ProseRefs P5]
		(StateTransform
			(prove (seller_refunded == 0))
			(prove ((buyer_paid == 100) or (buyer_paid == 200)))
			(seller_refunded' = buyer_paid)
		)
		(TransitionsTo Fulfilled)
	)

)