(Roles Landlord Tenant)

(TimeUnit d)

(ContractParams
	(LEASE_DURATION : TimeDelta := (days (4 * 29))) ;  approx 4 months
	(RENT : Nat = 1000)
	(MAX_RENT_DELAY : TimeDelta := 7D) ; 7 days
	(MIN_TERMINATION_NOTICE : TimeDelta := 30D) ; approx 1 month
)

(ProseContract
	(P1 'The term of this lease is for {LEASE_DURATION} months, beginning on {LEASE_TERM_START}. At the expiration of said term, the lease will automatically be renewed for a period of one month unless either party (Landlord or Tenant) notifies the other of its intention to terminate the lease at least one month before its expiration date.')
	(P2 'The lease is for 1 apartment, which is provided by Landlord throughout the term.')
	(P3 'Tenant agrees to pay the amount of {RENT} per month, each payment due within {MAX_DAYS_RENT_DELAY} days of the start of each month.')
)

(StateVars
	(lease_term_start : TimeDelta )
	(lease_term_end : TimeDelta )
	(writeOnceMore lease_terminated : Bool := false)
	(branchUnaffecting inconly total_rent_paid : Nat := 0)
)


(Macro RequestTerminationActionRuleMacro (RoleId SituationId)
	(if (not lease_terminated)
		(RoleId may (RequestTerminationFromSituationId) (within (min ((monthStartDay_td) + MAX_RENT_DELAY) (lease_term_end - MIN_TERMINATION_NOTICE))))
	)
)


(Macro RequestTerminationActionMacro SituationId
	(Action RequestTerminationFromSituationId (AllowedSubjects Landlord Tenant) (TransitionsTo SituationId)
		(StateTransform (lease_terminated' = true))
	)
)

(Dynamics
	"Lease Hvitved"
	(StartSituation Start)

	(Situation Start  (Landlord must (EnsureApartmentReady) immediately))

	(RequestTerminationActionMacro MonthStarted)
	(RequestTerminationActionMacro AfterPayRent)
	(RequestTerminationActionMacro MonthEnded)

	(Action EnsureApartmentReady (AllowedRoles Landlord))
	(Situation AfterEnsureApartmentReady (StartLeaseTerm immediately))

	(Action StartLeaseTerm (TransitionsTo MonthStarted)
		(StateTransform
			(lease_term_start' = last_event_td)
			(lease_term_end' = (last_event_td + LEASE_DURATION))
		)
	)

	(Action EnterMonthStarted (TransitionsTo MonthStarted))

	(Situation MonthStarted
		(Tenant may (PayRent) (within ((monthStartDay_td) + MAX_RENT_DELAY)))
		(Breach_Tenant (after ((monthStartDay_td) + MAX_RENT_DELAY)))
		(RequestTerminationActionRuleMacro (Tenant MonthStarted))
		(RequestTerminationActionRuleMacro (Landlord MonthStarted))
	)

	(Action PayRent (StateTransform (total_rent_paid' += 1000))
		(FollowingSituation
			(EnterMonthEnded (at (monthEndDay_td)))
			(RequestTerminationActionRuleMacro (Tenant AfterPayRent))
			(RequestTerminationActionRuleMacro (Landlord AfterPayRent))
		)
	)

	(Action EnterMonthEnded (AllowedSubjects Env)
		(1 ≤ traversals ≤ nonconstant)
		(FollowingSituation
			(Pre (last_situation_td ≤ lease_term_end))

			(if (lease_term_end ≤ (last_situation_td + 1D))
				(EnterLeaseTermEnded immediately)   )
			(if (lease_term_end > (last_situation_td + 1D))
				(EnterMonthStarted (at_split 1d))   )
			(RequestTerminationActionRuleMacro (Tenant MonthEnded))
			(RequestTerminationActionRuleMacro (Landlord MonthEnded))
		)
	)

	(Action EnterLeaseTermEnded (AllowedSubjects Env)
		(1 ≤ traversals ≤ nonconstant)
		(FollowingSituation
			(if lease_terminated (Tenant must (MoveOut) immediately))
			(if (not lease_terminated) (StartLeaseTerm immediately))
		)
	)

	(Action MoveOut (TransitionsTo Fulfilled) )
)