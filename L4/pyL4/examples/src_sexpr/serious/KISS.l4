
; QUESTION: Can the debt be repaid before maturity? If not, then the interest rate + maturity length is just an obtuse way of saying what will be owed if investor chooses payout.

; QUESTION: In Acquisition event if investor chooses 2x payment ("(b) get paid a multiple on the original investment."), does that mean the interest rate has no effect? NO -->

(Roles Investor Company)
(TimeUnit d)

(SortDefinitions
	($ := (Dimensioned NonnegReal "$"))
	(Pos$ := (Dimensioned PosReal "$"))
	(ShareCnt = (Dimensioned Nat "shares"))
	(PosShareCnt = (Dimensioned PosInt "shares"))
	(SharePrice = (Ratio $ PosShareCnt))
	(PosSharePrice = (Ratio Pos$ PosShareCnt))
)

(ContractParams
	(PURCHASE_AMOUNT : Pos$ = 100000)

	(TIME_TILL_MATURITY : TimeDelta = (days 547)) ; approx 18 months
	(VALUATION_CAP : Pos$ = 1000000000000)
	(DISCOUNT_RATE : "Fraction(0,1]" = 1)
	(INTEREST_RATE : "Fraction[0,1]" = .04)
	(DEBT_VERSION : Bool = true)

	(START_INVESTOR_COMMON_STOCK : ShareCnt = 0)
	(START_INVESTOR_PREFERRED_STOCK : ShareCnt = 0)
	(START_INVESTOR_SERIES_SEED_PREFERRED_STOCK : ShareCnt = 0)
	(START_INVESTOR_CASH : $ = 0)

	(START_COMPANY_CASH : $ = 0)
)

(StateVars
	(investor_cash : $ = START_INVESTOR_CASH)
	(investor_Common_Stock : ShareCnt = START_INVESTOR_COMMON_STOCK)
	(investor_PreferredStock : ShareCnt = START_INVESTOR_PREFERRED_STOCK)
	(investor_SeriesSeedPreferredStock : ShareCnt = START_INVESTOR_SERIES_SEED_PREFERRED_STOCK)

	(company_cash : $ = START_COMPANY_CASH)

	(writes≤1 pay_interest_in_cash : Bool = false)
)


(Macro compounded_full_periods (#princ #ir #periods)
	(#princ * ((1 + #ir) ^ #periods))
)


(Macro principle_and_compound_interest (#princ #ir #full_periods #final_period_fraction) (
	(compounded_full_periods (#princ #ir #full_periods)) +
	(((compounded_full_periods (#princ #ir #full_periods)) * #ir) * #final_period_fraction)
))
(Macro principle_and_compound_interest_lt_year (#princ #ir #final_period_fraction)
	(#princ + ((#princ * #ir) * #final_period_fraction))
)
(Macro principle_and_compound_interest_gt_year (#princ #ir #final_period_fraction) (
	(#princ * (1 + #ir)) +
	(((#princ * (1 + #ir)) * #ir) * #final_period_fraction)
))


(Macro compound_interest (#princ #ir #full_periods #final_period_fraction)
	(check Pos$ ((principle_and_compound_interest (#princ #ir #full_periods #final_period_fraction)) - #princ))
)
(Macro compound_interest_lt_year (#princ #ir #final_period_fraction)
	(check Pos$ ((principle_and_compound_interest_lt_year (#princ #ir #final_period_fraction)) - #princ))
)
(Macro compound_interest_gt_year (#princ #ir #final_period_fraction)
	(check Pos$ ((principle_and_compound_interest_gt_year (#princ #ir #final_period_fraction)) - #princ))
)


(Macro interest_from_td #td
	(ifthenelse (#td ≤ 365d)
		(compound_interest_lt_year (PURCHASE_AMOUNT INTEREST_RATE (#td / 365d)))
		(compound_interest_gt_year (PURCHASE_AMOUNT INTEREST_RATE ((trust PosTimeDelta (#td - 365d)) / 365d)))
	)
)

(Dynamics "KISS"
	(StartSituation InvestorInvests)

	(Situation InvestorInvests
		(Company may (CommitToAcquisition ?1) (before TIME_TILL_MATURITY))
		(Company may (CommitToFinancing ?1) (before TIME_TILL_MATURITY)) ; IPO
		(ReachMaturity (at_or_after TIME_TILL_MATURITY))
	)

	(Action (CommitToFinancing
				(qualifying_round_new_money : $)
				)
		; Automatic conversion to preferred stock when you raise a qualifying priced round.
		; Conversion price = lesser of cap or discount.
		(AllowedSubjects Company)
		(Pre (qualifying_round_new_money ≥ ($ 1000000)))

		(FollowingSituation
			(Company must NotifyInvestorOfNextEquityFinancingTerms)
		)
	)

	(Action NotifyInvestorOfNextEquityFinancingTerms
		(AllowedSubjects Company)
		(TransitionsTo After_NotifyInvestorOfNextEquityFinancingTerms)
	)

	(Situation After_NotifyInvestorOfNextEquityFinancingTerms
		(Investor may choose_cash_interest_repayment)
		(Company may (CloseEquityFinancingSale ?1 ?2) (after_split 5D))
		; discretionary Breach if never close?
	)

	(Action choose_cash_interest_repayment (TransitionsTo SRC)
		(AllowedSubjects Investor)
		(StateTransform
			(pay_interest_in_cash' = true)
		)

	)

	(Action (CloseEquityFinancingSale
				(qualifying_round_price : SharePrice)
				(company_capitalization : PosShareCnt)
		)
		(AllowedSubjects Company)
		(TransitionsTo Fulfilled)
		(StateTransform
			(local cap_price : SharePrice = (VALUATION_CAP / company_capitalization))
			(local discount_price : SharePrice = (qualifying_round_price * DISCOUNT_RATE)) ; check this!
			(local conversion_price : SharePrice = (min discount_price cap_price))
			(if pay_interest_in_cash (
				(local change : $ := (min (interest_from_td last_event_td) company_cash))
				(company_cash' = (check $ (company_cash - change)))
				(investor_cash' += change)
				(investor_PreferredStock' += (PURCHASE_AMOUNT round/ conversion_price))
			) else (
				(local accrued_interest : $ = (interest_from_td last_event_td))
				(investor_PreferredStock' += ((PURCHASE_AMOUNT + accrued_interest) round/ conversion_price))
			))

		)
	)


	(Action (CommitToAcquisition (company_capitalization : PosShareCnt))
		; Option to either (a) convert to common stock at the cap or (b) get paid a multiple on the original investment.
		(AllowedSubjects Company)

		(FollowingSituation
			(Investor may ChooseMultipleRepayment)
			(Investor may (ChooseConvertToCommonStock company_capitalization) )
			; KISS doesn't say there's a default, so...
			(Arbiter may InterveneOnDelay_Investor)
		)
	)

	(Action ChooseMultipleRepayment
		(AllowedSubjects Investor)
		(StateTransform
			; from Corporate Transaction Payment defn
			(local change : $ = (min ((interest_from_td last_event_td) + (2 * PURCHASE_AMOUNT)) company_cash))
			(company_cash' = (check $ (company_cash - change)))
			(investor_cash' += change)

		)
		(TransitionsTo Fulfilled)
	)

	(Action (ChooseConvertToCommonStock (company_capitalization : PosShareCnt))
		(AllowedSubjects Investor)
		(StateTransform
			(local cap_price : SharePrice = (VALUATION_CAP / company_capitalization))
			(investor_Common_Stock' += (PURCHASE_AMOUNT round/ cap_price))
		)
		(TransitionsTo Fulfilled)
	)

	(Action ReachMaturity
		; Option to convert to Series Seed Preferred Stock at the cap. If debt version, option for repayment.
		(AllowedSubjects Env)

		(FollowingSituation
			(if DEBT_VERSION
				(Investor may (ChooseConversionToSeriesSeedPreferredStock ?1))
			)
			(Investor may ChooseMaturityRepayment)
			; KISS doesn't say there's a default, so...
			(Arbiter may InterveneOnDelay_Investor)
		)
	)

	(Action ChooseMaturityRepayment
		(AllowedSubjects Investor)
		(StateTransform
			(local change : $ = (min ((interest_from_td last_event_td) + PURCHASE_AMOUNT) company_cash))
			(company_cash' = (check $ (company_cash - change)))
			(investor_cash' += change)
		)
		(TransitionsTo Fulfilled)
	)

	(Action (ChooseConversionToSeriesSeedPreferredStock (company_capitalization : PosShareCnt))
		(AllowedSubjects Investor)
		(StateTransform
			(local cap_price : SharePrice = (VALUATION_CAP / company_capitalization))
			(investor_SeriesSeedPreferredStock' += (PURCHASE_AMOUNT round/ cap_price))
		)
		(TransitionsTo Fulfilled)
	)

)
