; This L4 contract uses the boolean precompilation flags HAS_DISCOUNT and HAS_CAP.

; Need notation for

(Roles Investor Company)

(TimeUnit d)
(DefaultActionTimeLimit 2d)


(ContractParams
	(Purchase_Amount : Pos$ = 100000)
	(Discount_Rate : "Fraction(0,1]" = .9)
	(Discount : "Fraction[0,1)" = (1 - Discount_Rate))
	(Valuation_Cap : Pos$ = 100000000)
)


(NonoperativeContractParams
	(Governing_Law_Jurisdiction)
	(State_of_Incorporation)
	(Date_of_Safe)
)

(SortDefinitions
	(Δ$ := (Dimensioned Real "$"))
	($ := (Dimensioned NonnegReal "$"))
	(Pos$ := (Dimensioned PosReal "$"))
	(ShareCnt = (Dimensioned Nat "shares"))
	(ΔShareCnt = (Dimensioned Int "shares"))
	(PosShareCnt = (Dimensioned PosInt "shares"))
	(SharePrice = (Ratio $ PosShareCnt))
	(PosSharePrice = (Ratio Pos$ PosShareCnt))
	(Liquidity_Event_Type = (Enum Change_of_Control Initial_Public_Offering))
)

(NLGNames
	(Purchase_Amount "Purchase Amount")
	(Discount_Rate "Discount Rate")
	(Valuation_Cap "Valuation Cap")
	(investor_Common_Stock "Investor's Common Stock")
	(investor_SAFE_Stock "Investor's SAFE Stock")
	(investor_cash "Investor's Cash" )
	(unconverted_investment "Unconverted Investment")
	(Conversion_Price "Conversion Price")
	(company_cash "Company's Cash")
	(liq_cashout "Liquidity Cashout")
	(dis_cashout "Dissolution Cashout")
	(undiscounted_standard_preferred_stock_price "Undiscounted Preferred Stock Price")
	(Safe_Price "Cap Price")
	(Discount_Price "Discount Price")
	(investor_percent_of_cashout_investor_investments "Investor Cashout %")
	(investor_percent_of_dissolving_investor_investments "Investor Cashout %")
	(company_capitalization "Capitalization")
	(premoney_valuation "Valuation")
	(company_cash_at_liquidity_event "Total Cash Reserves")
	(company_cash_at_dissolution_event "Total Cash Reserves")
	(purchase_amount_reduction "Purchase Amount Reduction")
	(liquidity_capitalization "Liquidity Capitalization")
	(company_valuation "Valuation")
	(total_investments_of_cashout_investors "Total Investments of Cashout Investors")
	(reduction_needed_to_qualify_as_usa_tax_free_reorg "Reduction Needed to Qualify as USA Tax-free Reorganization")

	(?total_investments_of_cashout_investors "Total Investments of Cashout Investors")
)


(NLGSection
	"THIS INSTRUMENT AND ANY SECURITIES ISSUABLE PURSUANT HERETO HAVE NOT BEEN REGISTERED UNDER THE SECURITIES ACT OF 1933, AS AMENDED (THE “SECURITIES ACT”), OR UNDER THE SECURITIES LAWS OF CERTAIN STATES.  THESE SECURITIES MAY NOT BE OFFERED, SOLD OR OTHERWISE TRANSFERRED, PLEDGED OR HYPOTHECATED EXCEPT AS PERMITTED UNDER THE ACT AND APPLICABLE STATE SECURITIES LAWS PURSUANT TO AN EFFECTIVE REGISTRATION STATEMENT OR AN EXEMPTION THEREFROM.

	{val Company}

	SAFE
	(Simple Agreement for Future Equity)

	THIS CERTIFIES THAT in exchange for the payment by {val Investor} (the {Investor}) of ${val Purchase_Amount} ({Purchase_Amount}) on or about {val Date_of_Safe}, {val Company}, a {val State_of_Incorporation} corporation (the {Company}), hereby issues to the {Investor} the right to certain shares of the {Company}’s capital stock, subject to the terms set forth below.

	{Valuation_Cap} is ${val Valuation_Cap}.
	{Discount} is {val Discount}%.
	{Discount_Rate} is {val DiscountRate}."
)

(NLGSection
	"
	{Capital_Stock} means the capital stock of the {Company}, including, without limitation, {Common_Stock} and {Preferred_Stock}.

	{Change_of_Control} means (i) a transaction or series of related transactions in which any “person” or “group” (within the meaning of Section 13(d) and 14(d) of the Securities Exchange Act of 1934, as amended), becomes the “beneficial owner” (as defined in Rule 13d-3 under the Securities Exchange Act of 1934, as amended), directly or indirectly, of more than 50% of the outstanding voting securities of the {Company} having the right to vote for the election of members of the {Company}’s board of directors, (ii) any reorganization, merger or consolidation of the {Company}, other than a transaction or series of related transactions in which the holders of the voting securities of the {Company} outstanding immediately prior to such transaction or series of related transactions retain, immediately after such transaction or series of related transactions, at least a majority of the total voting power represented by the outstanding voting securities of the {Company} or such other surviving or resulting entity or (iii) a sale, lease or other disposition of all or substantially all of the assets of the {Company}.

	{Company_Capitalization} means the sum, as of immediately prior to the {Equity_Financing}, of: (1) all shares of {Capital_Stock} (on an as-converted basis) issued and outstanding, assuming exercise or conversion of all outstanding vested and unvested options, warrants and other convertible securities, but excluding (A) this instrument, (B) all other Safes, and (C) convertible promissory notes; and (2) all shares of {Common_Stock} reserved and available for future grant under any equity incentive or similar plan of the {Company}, and/or any equity incentive or similar plan to be created or increased in connection with the {Equity_Financing}.

	✓{Conversion_Price} means either: (1) the {Safe_Price} or (2) the {Discount_Price}, whichever calculation results in a greater number of shares of {Safe_Preferred_Stock}.

	✓{Discount_Price} means the price per share of the {Standard_Preferred_Stock} sold in the {Equity_Financing} multiplied by the {Discount_Rate}.

	{Distribution} means the transfer to holders of {Capital_Stock} by reason of their ownership thereof of cash or other property without consideration whether by way of dividend or otherwise, other than dividends on {Common_Stock} payable in {Common_Stock}, or the purchase or redemption of {Capital_Stock} by the {Company} or its subsidiaries for cash or property other than: (i) repurchases of {Common_Stock} held by employees, officers, directors or consultants of the {Company} or its subsidiaries pursuant to an agreement providing, as applicable, a right of first refusal or a right to repurchase shares upon termination of such service provider’s employment or services; or (ii) repurchases of {Capital_Stock} in connection with the settlement of disputes with any stockholder.

	✓{Dissolution_Event} means (i) a voluntary termination of operations, (ii) a general assignment for the benefit of the {Company}’s creditors or (iii) any other liquidation, dissolution or winding up of the {Company} (excluding a {Liquidity_Event}), whether voluntary or involuntary.

	{Equity_Financing} means a bona fide transaction or series of transactions with the principal purpose of raising capital, pursuant to which the {Company} issues and sells {Preferred_Stock} at a fixed pre-money valuation.

	{Initial_Public_Offering} means the closing of the {Company}’s first firm commitment underwritten initial public offering of {Common_Stock} pursuant to a registration statement filed under {Securities_Act}.

	{Liquidity_Capitalization} means the number, as of immediately prior to the {Liquidity_Event}, of shares of {Capital_Stock} (on an as-converted basis) outstanding, assuming exercise or conversion of all outstanding vested and unvested options, warrants and other convertible securities, but excluding: (i) shares of {Common_Stock} reserved and available for future grant under any equity incentive or similar plan; (ii) this instrument; (iii) other Safes; and (iv) convertible promissory notes.

	{Liquidity_Event} means a {Change_of_Control} or an {Initial_Public_Offering}.

	{Liquidity_Price} means the price per share equal to the {Valuation_Cap} divided by the {Liquidity_Capitalization}.

	{Pro_Rata_Rights_Agreement} means a written agreement between the {Company} and the {Investor} (and holders of other Safes, as appropriate) giving the Investor a right to purchase its pro rata share of private placements of securities by the Company occurring after the {Equity_Financing}, subject to customary exceptions.  Pro rata for purposes of the {Pro_Rata_Rights_Agreement} will be calculated based on the ratio of (1) the number of shares of {Capital_Stock} owned by the {Investor} immediately prior to the issuance of the securities to (2) the total number of shares of outstanding {Capital_Stock} on a fully diluted basis, calculated as of immediately prior to the issuance of the securities.

	{Safe} means an instrument containing a future right to shares of {Capital_Stock}, similar in form and content to this instrument, purchased by investors for the purpose of funding the Company’s business operations.

	{Safe_Preferred_Stock} means the shares of a series of {Preferred_Stock} issued to the {Investor} in an {Equity_Financing}, having the identical rights, privileges, preferences and restrictions as the shares of {Standard_Preferred_Stock}, other than with respect to: (i) the per share liquidation preference and the conversion price for purposes of price-based anti-dilution protection, which will equal the {Conversion_Price}; and (ii) the basis for any dividend rights, which will be based on the {Conversion_Price}.

	✓{Safe_Price}* means the price per share equal to the {Valuation_Cap} divided by the {Company_Capitalization}.

	{Standard_Preferred_Stock} means the shares of a series of {Preferred_Stock} issued to the investors investing new money in the {Company} in connection with the initial closing of the {Equity_Financing}.

	{Transaction_Documents_related_to_Equity_Financing} means all transaction documents related to the {Equity_Financing}; provided, that such documents are the same documents to be entered into with the purchasers of {Standard_Preferred_Stock}, with appropriate variations for the {Safe_Preferred_Stock} if applicable, and provided further, that such documents have customary exceptions to any drag-along applicable to the Investor, including, without limitation, limited representations and warranties and limited liability and indemnification obligations on the part of the Investor."
)

(NLGSection
	 "3.	Company Representations
		(a)	The {Company} is a corporation duly organized, validly existing and in good standing under the laws of the state of its incorporation, and has the power and authority to own, lease and operate its properties and carry on its business as now conducted.
		(b)	The execution, delivery and performance by the {Company} of this instrument is within the power of the {Company} and, other than with respect to the actions to be taken when equity is to be issued to the Investor, has been duly authorized by all necessary actions on the part of the {Company}. This instrument constitutes a legal, valid and binding obligation of the {Company}, enforceable against the {Company} in accordance with its terms, except as limited by bankruptcy, insolvency or other laws of general application relating to or affecting the enforcement of creditors’ rights generally and general principles of equity.  To the knowledge of the {Company}, it is not in violation of (i) its current certificate of incorporation or bylaws, (ii) any material statute, rule or regulation applicable to the {Company} or (iii) any material indenture or contract to which the {Company} is a party or by which it is bound, where, in each case, such violation or default, individually, or together with all such violations or defaults, could reasonably be expected to have a material adverse effect on the {Company}.
		(c)	The performance and consummation of the transactions contemplated by this instrument do not and will not: (i) violate any material judgment, statute, rule or regulation applicable to the {Company}; (ii) result in the acceleration of any material indenture or contract to which the {Company} is a party or by which it is bound; or (iii) result in the creation or imposition of any lien upon any property, asset or revenue of the {Company} or the suspension, forfeiture, or nonrenewal of any material permit, license or authorization applicable to the {Company}, its business or operations.
		(d)	No consents or approvals are required in connection with the performance of this instrument, other than: (i) the {Company}’s corporate approvals; (ii) any qualifications or filings under applicable securities laws; and (iii) necessary corporate approvals for the authorization of {Capital_Stock} issuable pursuant to Section 1.
		(e)	To its knowledge, the {Company} owns or possesses (or can obtain on commercially reasonable terms) sufficient legal rights to all patents, trademarks, service marks, trade names, copyrights, trade secrets, licenses, information, processes and other intellectual property rights necessary for its business as now conducted and as currently proposed to be conducted, without any conflict with, or infringement of the rights of, others."
)

(NLGSection
	"4.	Investor Representations
		(a)	The {Investor} has full legal capacity, power and authority to execute and deliver this instrument and to perform its obligations hereunder. This instrument constitutes valid and binding obligation of the {Investor}, enforceable in accordance with its terms, except as limited by bankruptcy, insolvency or other laws of general application relating to or affecting the enforcement of creditors’ rights generally and general principles of equity.
		(b)	The {Investor} is an accredited investor as such term is defined in Rule 501 of Regulation D under {Securities_Act}. The {Investor} has been advised that this instrument and the underlying securities have not been registered under {Securities_Act}, or any state securities laws and, therefore, cannot be resold unless they are registered under {Securities_Act} and applicable state securities laws or unless an exemption from such registration requirements is available. The {Investor} is purchasing this instrument and the securities to be acquired by the {Investor} hereunder for its own account for investment, not as a nominee or agent, and not with a view to, or for resale in connection with, the distribution thereof, and the {Investor} has no present intention of selling, granting any participation in, or otherwise distributing the same. The {Investor} has such knowledge and experience in financial and business matters that the {Investor} is capable of evaluating the merits and risks of such investment, is able to incur a complete loss of such investment without impairing the {Investor}’s financial condition and is able to bear the economic risk of such investment for an indefinite period of time."
)

(NLGSection
	"5.	Miscellaneous
		(a)	Any provision of this instrument may be amended, waived or modified only upon the written consent of the {Company} and the {Investor}.
		(b)	Any notice required or permitted by this instrument will be deemed sufficient when delivered personally or by overnight courier or sent by email to the relevant address listed on the signature page, or {48 hours} after being deposited in the U.S. mail as certified or registered mail with postage prepaid, addressed to the party to be notified at such party’s address listed on the signature page, as subsequently modified by written notice.
		(c)	The {Investor} is not entitled, as a holder of this instrument, to vote or receive dividends or be deemed the holder of Capital Stock for any purpose, nor will anything contained herein be construed to confer on the {Investor}, as such, any of the rights of a stockholder of the {Company} or any right to vote for the election of directors or upon any matter submitted to stockholders at any meeting thereof, or to give or withhold consent to any corporate action or to receive notice of meetings, or to receive subscription rights or otherwise until shares have been issued upon the terms described herein.
		(d)	Neither this instrument nor the rights contained herein may be assigned, by operation of law or otherwise, by either party without the prior written consent of the other; provided, however, that this instrument and/or the rights contained herein may be assigned without the {Company}’s consent by the {Investor} to any other entity who directly or indirectly, controls, is controlled by or is under common control with the {Investor}, including, without limitation, any general partner, managing member, officer or director of the {Investor}, or any venture capital fund now or hereafter existing which is controlled by one or more general partners or managing members of, or shares the same management company with, the {Investor}; and provided, further, that the {Company} may assign this instrument in whole, without the consent of the {Investor}, in connection with a reincorporation to change the {Company}’s domicile.
		(e)	In the event any one or more of the provisions of this instrument is for any reason held to be invalid, illegal or unenforceable, in whole or in part or in any respect, or in the event that any one or more of the provisions of this instrument operate or would prospectively operate to invalidate this instrument, then and in any such event, such provision(s) only will be deemed null and void and will not affect any other provision of this instrument and the remaining provisions of this instrument will remain operative and in full force and effect and will not be affected, prejudiced, or disturbed thereby.
		(f)	All rights and obligations hereunder will be governed by the laws of the State of {Governing_Law_Jurisdiction}, without regard to the conflicts of law provisions of such jurisdiction."
)



(StateVars
	(writes≤1 investor_Common_Stock: ΔShareCnt = 0)
	(writes≤1 investor_SAFE_Stock : ΔShareCnt = 0)
	(writes≤1 investor_cash : Δ$ = Purchase_Amount )

	; in a later version of L4, the type of the following variable might be (Uninitialized $),
	; which behaves just like $ after the first write, and causes a type error if there's a read before the first write.
	(unconverted_investment : $ = 0)

	; this is wrong, but testing:
	(writes≤1 Conversion_Price : PosSharePrice)

	(company_cash : $ = 0)

	; for Liquidity and Dissolution
	(writes≤1 company_cash_available_for_payout : $)

	; for Liquidity
	(writes≤1 liq_cashout : $ = 0) ; this needs to either be initialized to 0, or else changed to an Optional and some of the code rewritten a bit.

	; for Dissolution
	(writes≤1 dis_cashout : $)
)
; recall bug in a subcase of the case when the company cannot fully pay out all the investors who want to cash out.
(Invariants
	(liq_cashout ≤ unconverted_investment)
	(dis_cashout ≤ Purchase_Amount)
)


(ActionPredicate Notice (a)
	"(b)	Any notice required or permitted by this instrument will be deemed sufficient when delivered personally or by overnight courier or sent by email to the relevant address listed on the signature page, or {48 hours} after being deposited in the U.S. mail as certified or registered mail with postage prepaid, addressed to the party to be notified at such party’s address listed on the signature page, as subsequently modified by written notice."
	; Every off-chain message should have a send event and a receive event.
	; But, in most cases in an L4 contract, that detail should be hidden.
	; DeliveredByEmail
	; DeliveredByCourier
	; ISentByMail48HoursAgo

	; A Notice is either:
	; a pair of a send event and receive event, or
	; a blockchain send event only? or the receive event is when it's written to the chain
	; But L4 contract benefit from a happy path abstraction, which is like an instant verified direct message delivery.
	; idea:
	; (Company may (Send TransactionDocsWithPRA method))
)

(Dynamics "SAFE"
	; apply to all actions and states:
	; 	at-most-once ; static checker should verify that every action and state occurs at most once in any trace

	(StartSituation Signed)

	(Action ExternalShareCntChange

	)

	(Situation Signed
		(nlg "Investment Agreement Signed")
		(Investor may TransferInvestmentCash)
	)
	(Action TransferInvestmentCash (TransitionsTo InvestmentWaitingToConvert) (AllowedSubjects Investor)
		(nlg "Transfer Investment to Company")
		(StateTransform
			(investor_cash' = (investor_cash - Purchase_Amount))
			(company_cash' = (company_cash + Purchase_Amount))
			(unconverted_investment' = Purchase_Amount)
		)
	)

	(Situation InvestmentWaitingToConvert
		(nlg "Investment Waiting to Convert")
		(Company may (CommitToEquityFinancing))
		(Company may (CommitToIPO ?1 ?2))
		(Company may (CommitToChangeOfControl ?1 ?2 ?total_investments_of_cashout_investors ?4)
						(where (?total_investments_of_cashout_investors ≥ Purchase_Amount) )
		)
		(Company may CommitToDissolution)
	)

	; ----------------- {Equity_Financing} Event -------------------
	; NOTE: PRA abbreviates pro rata agreement

	(Action CommitToEquityFinancing (AllowedSubjects Company)
		(nlg "Initiate an {Equity_Financing} Event")
		(nlgsection "Equity Events")
		(FollowingSituation
			(Company may DeliverTransactionDocsWithPRA)
			(Company may DeliverTransactionDocsWithoutPRA)
			(Breach_Company (after_split 1d))
		)
	)

	(Action DeliverTransactionDocsWithPRA (AllowedSubjects Company) (TransitionsTo AfterPRASigned)
		(nlg "Deliver Transaction Docs With Pro Rata Rights Agreement (PRA)")
		(nlgsection "Equity Events")
		(sideeffects
			(Company sends Transaction_Documents_related_to_Equity_Financing to Investor)
		)
	)

	(Action DeliverTransactionDocsWithoutPRA (AllowedSubjects Company)
		(nlg "Deliver Transaction Docs (PRA to follow)")
		(nlgsection "Equity Events")
		(FollowingSituation
			(Company must SendProRataRightsAgreementForSigning)
		)
	)

	(Action SendProRataRightsAgreementForSigning  (AllowedSubjects Company)
		(nlg "Send PRA for Signing")
		(nlgsection "Equity Events")
		(FollowingSituation
			; TODO: Obviously this shouldn't be a *must*, since they ought to be
			; able to send it back with proposed amendments/corrections.
			; That seems like something we should have a reusable module for.
			(Investor must SignPRA)
		)
	)

	(Action SignPRA (AllowedSubjects Investor) (TransitionsTo AfterPRASigned)
		(nlg "Sign PRA")
		(nlgsection "Equity Events")
	)

	(Situation AfterPRASigned
		(nlg "After PRA Signed")
		(nlgsection "Equity Events")
		(Company must (IssueSAFEPreferredStock ?1 ?2))
	)

	(Action (IssueSAFEPreferredStock
				(Company_Capitalization : PosShareCnt)
				(company_valuation : Pos$) ) (AllowedSubjects Company)
		(nlg "Issue SAFE Preferred Stock")
		(nlgsection "Equity Events")
		(StateTransform
			(writeout undiscounted_standard_preferred_stock_price : SharePrice = (company_valuation / Company_Capitalization))
			(writeout Safe_Price : SharePrice = (Valuation_Cap / Company_Capitalization))
			(writeout Discount_Price : SharePrice = (undiscounted_standard_preferred_stock_price * Discount_Rate))
			(Conversion_Price' = (min Safe_Price Discount_Price))
			(investor_SAFE_Stock' = (investor_SAFE_Stock + (Purchase_Amount ceil/ Conversion_Price')))
		)
		(FollowingSituation
			(Company must Equity_Financing)
		)
	)

	(Action Equity_Financing (AllowedSubjects Company) (TransitionsTo Fulfilled)
		(nlg "Execute {Equity_Financing}")
		(nlgsection "Equity Events")
	)


	; ----------------- Liquidity Event -------------------

	(Situation InvestorToChooseMethodOfPayment
		(nlg "Investor to Choose Method of Payment")
		(nlgsection "Liquidity Events")
		(Investor may ChooseCashPayment [before_split 1w])
		(Investor may ChooseStockPayment [before_split 1w])
		(AutoChooseStockPayment [at_split 1w])
	)

	(Action (CommitToIPO (Liquidity_Capitalization : PosShareCnt)
						 (company_valuation : Pos$)
			) (AllowedSubjects Company)  (TransitionsTo InvestorToChooseMethodOfPayment)
		(nlg "Initiate an IPO Liquidity Event")
		(nlgsection "Liquidity Events")
		(StateTransform
			(writeout undiscounted_standard_preferred_stock_price : SharePrice = (company_valuation / Liquidity_Capitalization))
			(writeout Safe_Price : SharePrice = (Valuation_Cap / Liquidity_Capitalization))
			(writeout Discount_Price : SharePrice = (undiscounted_standard_preferred_stock_price * Discount_Rate))
			(Conversion_Price' = (min Safe_Price Discount_Price))
		)
	)

	(Action (CommitToChangeOfControl (Company_Capitalization : PosShareCnt)
								     (company_valuation : Pos$)
								     (total_investments_of_cashout_investors: Pos$)
								     (reduction_needed_to_qualify_as_usa_tax_free_reorg : $)

			) (AllowedSubjects Company) (TransitionsTo InvestorToChooseMethodOfPayment)
		(nlg "Initiate a Change-of-Control Liquidity Event")
		(nlgsection "Liquidity Events")
		(StateTransform
			(writeout undiscounted_standard_preferred_stock_price : SharePrice = (company_valuation / Company_Capitalization))
			(writeout Safe_Price : SharePrice = (Valuation_Cap / Company_Capitalization))
			(writeout Discount_Price : SharePrice = (undiscounted_standard_preferred_stock_price * Discount_Rate))
			(Conversion_Price' = (min Safe_Price Discount_Price))
			(local investor_percent_of_cashout_investor_investments : "Fraction(0,1]" = (check "Fraction(0,1]" (Purchase_Amount / total_investments_of_cashout_investors)))
		 	(local purchase_amount_reduction : $ = (investor_percent_of_cashout_investor_investments * reduction_needed_to_qualify_as_usa_tax_free_reorg))
			(investor_Common_Stock' += (purchase_amount_reduction ceil/ Conversion_Price'))
			(unconverted_investment' = (check Pos$ (unconverted_investment - purchase_amount_reduction)))
			(company_cash' = (check $ (company_cash - purchase_amount_reduction)))
		)
	)

	(Action AutoChooseStockPayment (AllowedSubjects Company)
		(nlg "Stock Payment Option Chosen by Default")
		(nlgsection "Liquidity Events")
		(FollowingSituation
			; what this translates to depends on if DefaultActionTimeLimit is defined
			(Company must TransferCommonStock)
		)
	)

	(Action ChooseStockPayment (AllowedSubjects Investor)
		(nlg "Choose Payment in Common Stock")
		(nlgsection "Liquidity Events")
		(FollowingSituation
			(Company must TransferCommonStock)
		)
	)

	(Action TransferCommonStock (AllowedSubjects Company)
		(nlg "Transfer Common Stock")
		(nlgsection "Liquidity Events")
		(StateTransform
			(investor_Common_Stock' += ((check $ (unconverted_investment - liq_cashout)) ceil/ Conversion_Price))
		)
		(FollowingSituation
			(Company must DoLiquidityEvent)
		)
	)

	(Action ChooseCashPayment (AllowedSubjects Investor)
		(nlg "Choose Payment in Cash")
		(nlgsection "Liquidity Events")
		(FollowingSituation
			(Company must (TransferCash_L ?1 ?2))
		)
	)

	(Action (TransferCash_L (company_cash_at_liquidity_event : $)
						    (total_investments_of_cashout_investors : Pos$)) (AllowedSubjects Company)
		(nlg "Transfer Cash (liquidity)")
		(nlgsection "Liquidity Events")
		(StateTransform
			(local investor_percent_of_cashout_investor_investments : "Fraction(0,1]" = (check "Fraction(0,1]" (Purchase_Amount / total_investments_of_cashout_investors)))
			(liq_cashout' = (min unconverted_investment
								 (company_cash_at_liquidity_event * investor_percent_of_cashout_investor_investments))
			)
			(investor_cash' += liq_cashout')
			(unconverted_investment' = (check $ (unconverted_investment - liq_cashout)))
			(company_cash' = (check $ (company_cash - liq_cashout')))
		)

		(FollowingSituation
			(if (liq_cashout < unconverted_investment)
				(Company must TransferCommonStock)
			)

			(if (liq_cashout == unconverted_investment)
				(Company must DoLiquidityEvent)
			)
		)
	)

	(Action DoLiquidityEvent (AllowedSubjects Company) (TransitionsTo Fulfilled)
		(nlg "Execute Liquidity Event")
		(nlgsection "Liquidity Events")
	)


	; ----------------- Dissolution Event -------------------

	(Action CommitToDissolution (AllowedSubjects Company)
		(nlg "Initiate a Dissolution Event")
		(nlgsection "Dissolution Events")
		(FollowingSituation
			(Company must
				(TransferCash_D ?company_cash_at_dissolution_event
								?investor_percent_of_dissolving_investor_investments)
			)
		)
	)


	(Action (TransferCash_D (company_cash_at_dissolution_event : $)
						    (investor_percent_of_dissolving_investor_investments : "Fraction(0,1]"))
		(AllowedSubjects Company)
		(nlg "Transfer Cash (dissolution)")
		(nlgsection "Dissolution Events")
		(StateTransform
			(dis_cashout' = (min
							Purchase_Amount
							(company_cash_at_dissolution_event * investor_percent_of_dissolving_investor_investments))
			)
			(investor_cash' = dis_cashout')
		)

		(FollowingSituation
			(if (dis_cashout < Purchase_Amount)
				(Company must (TransferAssets ?1 ?2))  )
			(if (dis_cashout == Purchase_Amount)
				(Company must Dissolution_Event)     )
		)
	)

	(Action (TransferAssets (Company_Capitalization : PosShareCnt)
							(company_valuation : Pos$) )
		(nlg "Transfer Assets")
		(nlgsection "Dissolution Events")
		(AllowedSubjects Company)
		(StateTransform
			(writeout undiscounted_standard_preferred_stock_price : SharePrice = (company_valuation / Company_Capitalization))
			(writeout Safe_Price : SharePrice = (Valuation_Cap / Company_Capitalization))
			(writeout Discount_Price : SharePrice = (undiscounted_standard_preferred_stock_price * Discount_Rate))
			(Conversion_Price' = (min Safe_Price Discount_Price))
			(investor_Common_Stock' += ((check $ (Purchase_Amount - dis_cashout)) ceil/ Conversion_Price'))
		)
		(FollowingSituation
			(Company must Dissolution_Event)
		)
	)


	(Action Dissolution_Event (AllowedSubjects Company) (TransitionsTo Fulfilled)
		(nlg "Execute Dissolution")
		(nlgsection "Dissolution Events")
	)

	(Situation Fulfilled)
)


; (EndOfTraceClaims
; If the contract ends with the investor having no change in stock and investor_cash < 0, then there must have been a dissolution event.
; 	(((investor_Common_Stock'== 0) and ((investor_SAFE_Stock == 0) and (investor_cash < 0))) -> (last_event_name == Dissolution_Event))
; )
