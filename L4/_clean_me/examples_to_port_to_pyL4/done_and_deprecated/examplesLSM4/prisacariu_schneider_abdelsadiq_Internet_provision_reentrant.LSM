(LSMVersion 4.0.0)

; These are some properties we would want to verify are true:
; Client, when their usage is high, should always have the permission to lower it. This one led me to catch a bug in an earlier version of the contract.
; Client, when they owe money, should always have the permission to pay the full amount owed.
; Client, if they neither pay within 24H nor send a delay-payment email within 24H, breaches the contract.
; Client, if they lower within 24H of infraction AND pay 1 fee unit within 24H of infraction, owes nothing more. This is regardless of whether they send a delay-payment email.
; Client, if they send delay email and lower traffic within 24H of infraction, and don't pay within 24 hours, then they owe and must eventually (say, within 48H of infraction) pay 2 fee units for this infraction.
; Client, if they send delay email and lower traffic between 24H and 48H after infraction, they owe and must eventually (say, within 48H of infraction) pay 3 fee units for this infraction.
; Client may pay in up to 2 parts, one before 24H and the other between 24H and 48H.
; Client, if they don't lower traffic within 48 hours of infraction, breaches the contract.

; NOTE 1: If Client overpays, this contract does not have any facility for refunding them (but it could easily be added).
; NOTE 2: This contract ignores the possibility of a message being sent but not received.

(FormalContract
	"PS Internet Provision"

	(Actors ISP Client)

	(ContractParams
		(HIGH_USAGE_FEE : ℕ := 10)
		(HOURS_PARAM : ℕ := 24)
	)

	; The original (ambiguous) agreement.
	(ProseContract
		(P1 'Whenever Internet traffic is high then the client must pay {HIGH_USAGE_FEE} or the client must notify the service provider by sending an e-mail specifying that he will pay later within {HOURS_PARAM} hours.')
		(P2 'In case the client delays the payment, after notification he must lower Internet traffic within {HOURS_PARAM} hours to the low level, and pay twice the amount {HIGH_USAGE_FEE}.')
		(P3 'If the client does not lower the Internet traffic within {HOURS_PARAM} hours, then the client will have to pay thrice the amount {HIGH_USAGE_FEE}.')
		(P4 'The provider does not have permission to cancel the contract without previous written notification by normal post and by e-mail.')
	)

	(GlobalVars
		(change≤once contract_canceled : Bool = false)
		(change≤once cancel_notice_sent_by_post : Bool = false)
		(change≤once cancel_notice_sent_by_email : Bool = false)

		; when they lower their usage level.
		(change≤once when_lowered : [Enum never before24H after24H] = never)

		; sent delay-payment-notification email within 24H of infraction.
		(change≤once sent_delay_email : Bool = false)

		; paid 1X high usage fee within 24H of infraction
		(change≤once paidBefore24 : Bool = false)

		; Client paid this much between 24 and 48 hours of infraction
		(change≤once paidAfter24 : [Enum nothing x2 x3] = nothing) ; how much
	)

	(StartState UsageWithinAllowance)

	(State UsageWithinAllowance
		; When ‹Client's internet traffic usage level is within the allowed range›
		; they may, by increasing their traffic, ‹cause their usage level to exceed
		; the allowed range›, after which the rules of section UsageLimitExceeded
		; go into force.
		(visits == 1)
		(Client may next (ExceedUsageLimit))
	)

	(Action CancelContract
		; ISP can ‹initiate termination of Client's account› whenever:
		; * ‹they have received a cancelation notice from Client by email›
		; * ‹they have received a cancelation notice from Client by post›
		; * ‹termination of Client's account has not already been initiated›
		(AllowedSubjects ISP)
		(traversals ≤ 1)
		(ISP may in any state if
			(and cancel_notice_sent_by_email
				 cancel_notice_sent_by_post
				 (not contract_canceled)
			)
		)
		(Entrance
			(contract_canceled = true)
		)
	)

	(Action LowerTrafficWithin24HofInfraction
		(AllowedSubjects Client)
		(traversals ≤ 1)
		(Entrance
			(when_lowered := before24H)
		)
		(if (not contract_canceled)
			(startInstance "PS Internet Provision" (Actors ISP Client))
		)
	)

	(Action SendDelayEmail
		(AllowedSubjects Client)
		(traversals ≤ 1)
		(Entrance
			(sent_delay_email := true)
		)
	)

	(Action Pay1XHighUsageFeeWithin24HofInfraction
		(AllowedSubjects Client)
		(traversals ≤ 1)
		(Entrance
			(paidBefore24 := true)
		)
	)

	(Action/State ExceedUsageLimit/UsageLimitExceeded
		; Within 24 hours of the Client exceeding the allowed internet
		; usage limit, they may next
		; * Lower their usage, if they haven't done so already, and/or
		; * Send an email to the ISP notifying them that they will delay their
		;	high usage fee payment, if they haven't done so already, and/or
		; * Pay the ISP the high usage fee, if they haven't done so already.
		;
		; After 24 hours, the rules of this section no longer apply, and
		; the rules of section After24H go into force.
		(AllowedSubjects Client)
		(visits ≤ 1)
		(verifiedDisjointExhaustive
			(if (within? 24H)
				(Client may next (LowerTrafficWithin24HofInfraction) if (when_lowered != before24H))
				(Client may next (SendDelayEmail) if (not sent_delay_email))
				(Client may next (Pay1XHighUsageFeeWithin24HofInfraction) if (not paidBefore24))
			)
			(if (not (within? 24H))
				(After24H)
			)
		)
	)

	(Event/State After24H
		; When Section After24H is in force:
		; ~~> see individual clauses for sketched NLG
		(visits ≤ 1)
		; note visits ≤ 1 instead of == 1 because the contract technically
		; allows the possibility that the client delivers their cancelation
		; request messages, and the ISP cancels their service, all before
		; they raise their internet traffic.
		(verifiedDisjoint
			(if (and paidBefore24 (when_lowered == before24H))
				; If the Client both paid the high usage fee and lowered their usage
				; within 24H, then they owe nothing more, and the contract is fulfilled
				(ContingentFulfilled)
			)
			(if (and (not paidBefore24) sent_delay_email (when_lowered == before24H))
				; If the Client did not pay the high usage fee within 24 hours of their
				; infraction, and sent a delay-payment-notification email, and lowered
				; their internet usage within 24H, then they must pay 2x the high usage
				; fee within 48 hours of their infraction.
				; The contract is fulfilled as soon as all obligations are met.
				(if (paidAfter24 == nothing)
					(Client must later (within 24H) (Pay2XHighUsageFeeBetween24And48HofInfraction))
				)
				(ContingentFulfilled)
			)
			(if (and paidBefore24 (when_lowered != before24H))
				; If the Client paid the high usage fee within 24H and doesn't lower
				; traffic within 24H, then (regardless of whether they sent the delay
				; payment email), they must pay (an additional) 2X high usage fee
				; (for a total of 3X) within 48 hours of their infraction (i.e. within
				; 24 hours of entering the After24H state).	The Client must also
				; lower their usage within 48 hours of their
				; infraction (i.e. within 24 hours of entering the After24H state).
				; The contract is fulfilled as soon as all obligations are met.
				(if (paidAfter24 == nothing)
					(Client must later (within 24H) (Pay2XHighUsageFeeBetween24And48HofInfraction))
				)
				(if (when_lowered != after24H)
					(Client must later (within 24H) (LowerTrafficBetween24And48HofInfraction))
				)
				(ContingentFulfilled)
			)
			(if (and (not paidBefore24) sent_delay_email (when_lowered != before24H))
				; If the client did not pay the high usage fee within 24 hours of their
				; infraction, and sent a delay-payment-notification email, and didn't
				; lower their usage within 24H of their infraction, then they must
				; pay 3X high usage fee. They must also lower their usage within 48
				; hours of their infraction (i.e. within 24 hours of entering the
				; After24H state).
				; The contract is fulfilled as soon as all obligations are met.
				(if (not F3_24_48)
					(Client must later (within 24H) (Pay3XHighUsageFeeBetween24And48HofInfraction))
				)
				(if (not L48)
					(Client must later (within 24H) (LowerTrafficBetween24And48HofInfraction))
				)
				(ContingentFulfilled)
			)
		)

		(if (when_lowered == never)
			(Client may next (within 24H) LowerTrafficBetween24And48HofInfraction)
		)
		(if (paidAfter24 == nothing)
			(Client may next (within 24H) Pay2XHighUsageFeeBetween24And48HofInfraction)
			(Client may next (within 24H) Pay3XHighUsageFeeBetween24And48HofInfraction)
		)

		(if contract_canceled
			(ContingentFulfilled)
		)
	)

	(Action Pay2XHighUsageFeeBetween24And48HofInfraction
		(AllowedSubjects Client)
		(traversals ≤ 1)
		(Entrance
			(paidAfter24 := x2)
		)
	)

	(Action Pay3XHighUsageFeeBetween24And48HofInfraction
		(AllowedSubjects Client)
		(traversals ≤ 1)
		(Entrance
			(paidAfter24 := x3)
		)
	)

	(Action LowerTrafficBetween24And48HofInfraction
		(AllowedSubjects Client)
		(traversals ≤ 1)
		(Entrance
			(when_lowered := after24H)
		)
		(if (not contract_canceled)
			(startInstance "PS Internet Provision" (Actors ISP Client))
		)
	)


)
